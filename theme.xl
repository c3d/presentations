import Slides
import Animate
import Charts
import VLCAudioVideo
import ObjectLoader
import RegExp
import Filters


theme "RedHat"

presentation_title X -> presentation_title := X
presentation_title -> "Presentation title not set"

presentation_logo X -> presentation_logo := X
presentation_logo -> "XLLogo.png"

presentation_url X -> presentation_url := X
presentation_url -> "https://github.com/c3d/redhat-template"

presentation_tao X -> presentation_tao := X
presentation_tao -> "https://github.com/c3d/redhat-template.git"

presentation_author X -> presentation_author := X
presentation_author -> "Christophe de Dinechin (dinechin@redhat.com, IRC c3d)"

presentation_author_title X -> presentation_author_title := X
presentation_author_title -> "Senior Principal Software Engineer"

presentation_author_mail X -> presentation_author_mail := X
presentation_author_mail -> "dinechin@redhat.com"



// ============================================================================
//
//    Customizations for the slides
//
// ============================================================================
//    This should be kept sorted with the more specific patterns at the top

theme_align "RedHat", "main", "title"           -> align_left
theme_align "RedHat", "main", "subtitle"        -> align_left
theme_align "RedHat", "section", "title"        -> align_left; vertical_align_bottom
theme_align "RedHat", "section", "subtitle"     -> align_left
theme_color "RedHat", "main", Theme             -> color "white"
theme_color "RedHat", "section", "title"        -> color redhat_red
theme_color "RedHat", "section", Theme          -> color "black"

theme_font  "RedHat", "movie", "title"          -> font "Overpass", bold, 60
theme_size  "RedHat", "movie", "title"          -> 60
theme_color "RedHat", "movie", "title"          -> color "white"
theme_align "RedHat", "movie", "title"          -> align 50%; vertical_align 50%

theme_font  "RedHat", "movie", "subtitle"       -> font "Overpass", bold, 45
theme_size  "RedHat", "movie", "subtitle"       -> 45
theme_color "RedHat", "movie", "subtitle"       -> color "white"
theme_align "RedHat", "movie", "subtitle"       -> align 50%; vertical_align 50%

theme_font  "RedHat", Master, "title"           -> font "Overpass"
theme_size  "RedHat", Master, "title"           -> 85
theme_font  "RedHat", Master, "subtitle"        -> font "Overpass"
theme_size  "RedHat", Master, "subtitle"        -> 50
theme_color "RedHat", Master, "subtitle"        -> color "#A21400"
theme_font  "RedHat", Master, "code"            -> font "Menlo", 40
theme_size  "RedHat", Master, "code"            -> 20
theme_color "RedHat", Master, "code"            -> color "#003388"
theme_font  "RedHat", Master, "boxtitle"        -> font "Overpass", bold, 40
theme_align "RedHat", Master, "boxtitle"        -> align 80%

theme_align "RedHat", Master, "caption-white"   -> align 50%; vertical_align 50%
theme_font  "RedHat", Master, "caption-white"   -> font "Overpass", bold, 30
theme_size  "RedHat", Master, "caption-white"   -> 30
theme_color "RedHat", Master, "caption-white"   -> color "white"

theme_align "RedHat", Master,"story",1          -> margins 40, 0; paragraph_space 0, 20
theme_align "RedHat", Master,"story",N          -> margins 40*N, 0
theme_color "RedHat", Master,"story",N          -> color "black", 0.99-0.2*N
theme_size  "RedHat", Master,"story",N          -> 40 - N * 2

theme_color "RedHat", Master,"picture"          -> color "white"
theme_font  "RedHat", Master,"story"            -> font "Overpass", "Arial"
theme_size  "RedHat", Master,"story"            -> 40

theme_font  "RedHat", Master, Theme             -> font "Overpass", "Arial"
theme_color "RedHat", Master, Style             -> color "black"



// ============================================================================
//
//    Charts
//
// ============================================================================

// Chart colors
theme_color "RedHat", Master:text, "line", N:integer -> line_color_hsv (336.0 * N) / chart_datasets_count, 0.3, 1.0, 1.0
theme_color "RedHat", "pie", Style:text, N:integer -> color_hsv (336.0 * N) / (chart_count chart_first), 0.3, 1.0, 1.0
theme_color "RedHat", "area", Style:text, N:integer -> theme_color "RedHat", "bar", Style, N
theme_color "RedHat", "line", Style:text, N:integer -> theme_color "RedHat", "bar", Style, N
theme_color "RedHat", "bar", Style:text, N:integer -> color_hsv (336.0 * N) / chart_datasets_count, 0.3, 1.0, 1.0

// No boxes for charts components (except title)
theme_box "RedHat",Master:text,"chart_legend", X:real,Y:real,W:real,H:real -> false
theme_box "RedHat",Master:text,"area", X:real,Y:real,W:real,H:real -> false
theme_box "RedHat",Master:text,"bar", X:real,Y:real,W:real,H:real -> false
theme_box "RedHat",Master:text,"line", X:real,Y:real,W:real,H:real -> false
theme_box "RedHat",Master:text,"pie", X:real,Y:real,W:real,H:real -> false

// No boxes for pictures either
theme_box "RedHat",Master:text,"picture", X:real,Y:real,W:real,H:real -> false
theme_box "RedHat",Master:text,"leftpic", X:real,Y:real,W:real,H:real -> false
theme_box "RedHat",Master:text,"rightpic", X:real,Y:real,W:real,H:real -> false


// ============================================================================
//
//    Theme definition
//
// ============================================================================

theme_slide_width  "RedHat" -> 1920
theme_slide_height "RedHat" -> 1080

// Red Hat colors
redhat_red      -> "#EE2000"
redhat_red1     -> "#8F1000"
redhat_red2     -> "#B21600"
redhat_red3     -> "#CC1B00"

redhat_orange1  -> "#DF4600"
redhat_orange2  -> "#E55520"
redhat_orange3  -> "#F07520"
redhat_orange4  -> "#DD3311"

redhat_blue     -> "#3889ED"
redhat_blue1    -> "#10008F"
redhat_blue2    -> "#10206F"
redhat_ocean1   -> "#8CF"

redhat_green1   -> "#008F10"
redhat_yellow1  -> "#FFBC33"

theme_main_background Body ->
// ----------------------------------------------------------------------------
//   Draw the triangles in the background
// ----------------------------------------------------------------------------
    clear_color "#A21400"

    background -250,
        translate_y -120
        shader_show "shaders/clouds.fs",
            shader_resolution
            shader_time
            shader_depth 100%
            shader_scale 40%
            shader_channel 0, "tex16.png"

    background -200,
        blend_function_separate "dst_color", "src_color", "src_alpha", "dst_alpha"
        color "#D20400", 80%
        rotate_z 45
        rectangle 3000, 1600

    background -100,
        blend_function_separate "dst_color", "src_color", "src_alpha", "dst_alpha"
        color redhat_red1, 70%
        rotate_z 45
        rectangle 0, 700, 1800, 100
    background -50,
        blend_function_separate "dst_color", "dst_color", "src_alpha", "dst_alpha"
        translate 510, -510, 0
        frame_texture 80, 80,
            line_color "white"
            color "transparent"
            line_width 2
            for I in 0..8 loop
                rectangle 10 * I, 10 * I
        texture_transform
            translate -seconds_time * 0.2 mod 1, 0.0, 0
            scale 20 + 10 * sin (0.1 * seconds_time)
        texture_wrap true, true
        color redhat_red2, 80%
        redhat_triangle 800, 880
        redhat_triangle A, B ->
            path
                move_to -B, -B
                line_to  B,  B
                line_to  B,  A
                line_to -A, -B
                line_to -B, -B

    Body

    contents -10,
        color "white"
        image -370, 300, 25%, 25%, "RedHat-WhiteText.png"


title_slide_author Picture:text, Body ->
// ----------------------------------------------------------------------------
//    Draw a small text box with the presenter's name and info
// ----------------------------------------------------------------------------
    contents 100,
        color "white"
        texture Picture
        circle -550, -350, 90
    contents 10,
        text_box -100, -400, 600, 200,
            font "Overpass", 30
            color "white"
            align 0%
            vertical_align 0%
            render Body
        color "red"
        rectangle -350, -280, 100, 4


theme_background "RedHat", "main" ->
// ----------------------------------------------------------------------------
//   Draw the triangles in the background
// ----------------------------------------------------------------------------
    theme_main_background false


theme_background "RedHat", "section" ->
// ----------------------------------------------------------------------------
//   Same as section
// ----------------------------------------------------------------------------
    clear_color "#EFEFEF"
    background -250,
        translate_y -120
        shader_show "shaders/clouds.fs",
            shader_resolution
            shader_time
            shader_depth 100%
            shader_scale 40%
            shader_channel 0, "tex16.png"
    background -200,
        color "white"
        texture "Red Hat Background.png"
        section_rectangle texture_width * slide_height / texture_height, slide_height
        section_rectangle W:real, H:real ->
            locally
                texture ""
                color "#EE0000", 80%
                rectangle -(slide_width - W)/2, 0, W, H
            color "white", 99%
            rectangle -(slide_width - W)/2, 0, W, H

    contents -50,
        color "white"
        image 800, -510, 15%, 15%, "RedHat-BlackText.png"

    contents -50,
        color "#FFF"
        image 600, 410, 40%, 40%, "devconf_logo.png"



theme_background "RedHat", "movie" ->
// ----------------------------------------------------------------------------
//   No background for full-screen movies
// ----------------------------------------------------------------------------
    false


theme_background "RedHat", Master ->
// ----------------------------------------------------------------------------
//   Draw the triangles in the background
// ----------------------------------------------------------------------------
    clear_color "white"
    on "pageentry",
        refresh 0.1
    background -200,
        frame_texture 80, 80,
            line_color "white"
            color "transparent"
            line_width 2
            for I in 0..8 loop
                rectangle 10 * I, 10 * I
        texture_transform
            translate -seconds_time * 0.2 mod 1, 0.0, 0
            scale 20 + 10 * sin (0.1 * seconds_time)
        texture_wrap true, true
        color "lightgray", 30%
        locally
            translate 620, -620, 0
            redhat_triangle 800, 860
        locally
            translate -620, 620, 0
            redhat_triangle 800, 860
        redhat_triangle A, B ->
            path
                move_to -B, -B
                line_to  B,  B
                line_to  B,  A
                line_to -A, -B
                line_to -B, -B
    contents -100,
        color "lightgray"
        rectangle 0, -480, 1700, 2
    contents -50,
        color "white"
        image 800, -510, 15%, 15%, "RedHat-BlackText.png"
    contents -50,
        color "#FFF"
        image 550, -510, 20%, 20%, "devconf_logo.png"
    contents -50,
        box -790, -510, 120, 50,
            vertical_align 0.5
            font_size 22
            text "Page " & page_number
            if step > 0 then text "." & step
            on "pageentry",
                refresh 0.1
    contents 0,
        box 0, -510, 1400, 50,
            vertical_align 0.5
            font_size 22
            text presentation_title
    contents -20,
        box 0, -510, 800, 50,
            vertical_align 0.5
            font_size 22
            align_right
            text month & "/" & day & "/" & year & " - " & hours & ":" & zero minutes & ":" & zero seconds
    zero N -> if N < 10 then "0" & N else N


theme_boxstyle "RedHat","movie",Style:text->
// ----------------------------------------------------------------------------
//   Style for text boxes
// ----------------------------------------------------------------------------
    color "black", 30%


theme_boxstyle "RedHat",Master:text,Style:text->
// ----------------------------------------------------------------------------
//   Style for text boxes
// ----------------------------------------------------------------------------
    color "black"


theme_box "RedHat","movie",Style:text, X:real,Y:real,W:real,H:real ->
// ----------------------------------------------------------------------------
//   Draw the background box for text and flows in movie slides
// ----------------------------------------------------------------------------
    contents 80,
        theme_boxstyle "RedHat", "movie", Style
        rectangle X, Y, W, H


theme_text Theme:text,"movie",Style:text,X:real,Y:real,W:real,H:real,Body ->
// ----------------------------------------------------------------------------
//   Draw a default text box
// ----------------------------------------------------------------------------
    theme_box Theme, "movie", Style, X, Y, W, H
    contents 100,
        text_box X, Y, W, H,
            theme_style Theme, "movie", Style
            Body


theme_box "RedHat",Master:text,Style:text, X:real,Y:real,W:real,H:real ->
// ----------------------------------------------------------------------------
//   Draw the background box for text and flows
// ----------------------------------------------------------------------------
    false


movie_name -> ""
movie_timing -> 0
movie_slide Title:text, Name:text, Body ->
// ----------------------------------------------------------------------------
//   Create a slide containing a full-screen movie
// ----------------------------------------------------------------------------
    slide_master theme, "movie", Title,
        movie_name := Name
        title page_label
        stepping
        Body
        full_screen_movie 1, Name
        if movie_timing = 1 then
            contents 200,
                text_box 800, 500, 200, 50,
                    align 0%
                    vertical_align 0%
                    font "Arial", 20
                    color "red"
                    text "Time=" & movie_time movie_name
        on "key:Control-T",
            movie_timing := 1 - movie_timing
movie_slide Title:text, Name:text -> movie_slide Title, Name, false


movie_highlight N:real..M:real, X:real, Y:real, W:real, H:real, Body ->
// ----------------------------------------------------------------------------
//    Higlight a region of the screen based on current movie time
// ----------------------------------------------------------------------------
    if movie_time movie_name in N-0.5..M+0.5 then
        contents 90,
            show fade_between(movie_time movie_name, N+0.5, M-0.5)
            color "transparent"
            line_color_hsv 90 + 20 * sin time, 100%, 100%, 90%
            line_width 5
            rectangle X, Y, W, H

            color "black", 75%
            line_width 0
            rectangle X, Y + H/2 + 500, 4000, 1000
            rectangle X, Y - H/2 - 500, 4000, 1000
            rectangle X + W/2 + 1000, Y, 2000, H
            rectangle X - W/2 - 1000, Y, 2000, H
            text_box X, Y + H/2 + 50, W-10, 100,
                align 50%
                vertical_align 95%
                font "Arial Rounded MT Bold", 30
                color "yellow"
                render Body



// ============================================================================
//
//    Layout for the various kind of pages
//
// ============================================================================

theme_layout "RedHat", "main" ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flows 200, 0, 1200, 400, "title", "subtitle"


theme_layout "RedHat", "section" ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow 400,  250, 1000, 450, "title"
    flow 400, -150, 1000, 350, "subtitle"


theme_layout "RedHat", "movie" ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow   slide_width * 0.25, -slide_height * 0.1, slide_width * 0.5,  80, "title"
    flow   slide_width * 0.25, -slide_height * 0.2, slide_width * 0.5, 80, "subtitle"


theme_layout "RedHat", "blank" ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow 0, 0, 1800, 750, "story"


theme_layout "RedHat", Master:text ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flows   0,  400, 1800, 300, "title", "subtitle"
    flow    0,  -80, 1800, 700, "story"
    flow -475,  -80,  850, 700, "left"
    flow  475,  -80,  850, 700, "right"


slide_picture_at A..B, What ->
// ----------------------------------------------------------------------------
//   Show the "What" picture at time "When"
// ----------------------------------------------------------------------------
    only_at A..B,
        slide_picture What


slide_picture_at When, What ->
// ----------------------------------------------------------------------------
//   Show the "What" picture at time "When"
// ----------------------------------------------------------------------------
    only_at When,
        slide_picture What


slide_picture Picture:text -> slide_picture Picture, 0%
slide_picture Picture:text, Round:real -> slide_picture Picture, Round, 200
slide_picture Picture:text, Round:real, Size:real ->
// ----------------------------------------------------------------------------
//   Show a picture in a slide
// ----------------------------------------------------------------------------
    contents 100,
        translate 900-Size/2, 350, 0
        texture Picture
        color "white"
        scale min(Size / texture_width, Size / texture_height)
        rounded_rectangle 0, 0, texture_width, texture_height, Round * max(texture_width, texture_height)


slide_movie Movie:text -> slide_movie Movie, 0%
slide_movie Movie:text, Round:real ->
// ----------------------------------------------------------------------------
//   Show a picture in a slide
// ----------------------------------------------------------------------------
    contents 100,
        translate 700, 420, 0
        movie_texture Movie
        color "white"
        rotate_z -3
        if texture_width > 0 and texture_height > 0 then
            scale min(300.0 / texture_width, 300.0 / texture_height)
            rounded_rectangle 0, 0, texture_width, texture_height, Round * max(texture_width, texture_height)
        on "pageexit",
            movie_drop Movie


slide_shader Shader:text -> slide_shader Shader, false
slide_shader Shader:text, Body ->
// ----------------------------------------------------------------------------
//   Show a picture in a slide
// ----------------------------------------------------------------------------
    contents 100,
        translate 800, 400, 0
        frame_texture 256, 256,
            clear_color "transparent"
            shader_program
                fragment_shader_file Shader
            shader_set iViewpoint := 0
            shader_set iGlobalTime := page_time
            shader_set iResolution := 256, 256, 1
            shader_channel 0, "tex04.jpg"
            Body
            color "white"
            rectangle 256, 256
        color "white"
        rectangle 256, 256

title_slide_logo ->
// ----------------------------------------------------------------------------
//   The logo on the title slides
// ----------------------------------------------------------------------------
    picture
        translate 600, 420, 20
        texture presentation_logo
        color "white"
        title_slide_logo_internal texture_width, texture_height
        title_slide_logo_internal 0, H -> false
        title_slide_logo_internal W, 0 -> false
        title_slide_logo_internal W:integer, H:integer ->
            scale min(400.0 / W, 300.0 / H)
            locally
                texture ""
                rounded_rectangle 0, 0, W*120%, H*120%, 60
            rounded_rectangle 0, 0, W, H, 0



// ============================================================================
//
//    Utilities
//
// ============================================================================

current_step -> 1

stepping ->
// ----------------------------------------------------------------------------
//   A body where each bullet is at a given step
// ----------------------------------------------------------------------------
    compute_smooth_step
    current_step := 1
    on "pageentry",
        skip_directly_to 0
        highest_step := 0


one_picture Body ->
// ----------------------------------------------------------------------------
//   Just draw a single picture
// ----------------------------------------------------------------------------
    picture
        at_step 1
        rotate_y 90 * fade_out_at(smooth_step, 1)
        Body


at_current_step ->
// ----------------------------------------------------------------------------
//   Show something at the current step
// ----------------------------------------------------------------------------
    at_step current_step
    current_step := current_step + 1


only_at N:integer..M:integer, Body ->
// ----------------------------------------------------------------------------
//   Show something at step N only
// ----------------------------------------------------------------------------
    if smooth_step in N-1..M+1 then
        locally
            show 1.3*fade_between(smooth_step, N, M)
            Body


only_at N:integer, Body ->
// ----------------------------------------------------------------------------
//   Show something at step N only
// ----------------------------------------------------------------------------
    if smooth_step in N-1..N+1 then
        locally
            show 1.3*fade_between(smooth_step, N, N)
            Body


*    Body -> at_current_step; bullet 1, Body
**   Body -> at_current_step; bullet 2, Body
***  Body -> at_current_step; bullet 3, Body
**** Body -> at_current_step; bullet 4, Body

$    -> $ " "
$ X  -> paragraph X

top_left     Body -> box -475,  150, 900, 350, { render Body }
top_right    Body -> box  475,  150, 900, 350, { render Body }
bottom_left  Body -> box -475, -250, 900, 350, { render Body }
bottom_right Body -> box  475, -250, 900, 350, { render Body }
// ----------------------------------------------------------------------------
//   Draw a box in the four quadrants
// ----------------------------------------------------------------------------


! Body ->
// ----------------------------------------------------------------------------
//   Highlight a given bullet point
// ----------------------------------------------------------------------------
    * { color_hsv 32.3 * seconds, 0.4, 1; render Body }


highlight       -> "none"
highlight_start -> "none"
highlight_end   -> "none"
highlight S,E,H -> highlight_start := S; highlight_end := E; highlight := H
highlight S, E  -> highlight S, E, "none"
highlight H     -> highlight "none", "none", H
highlighting    -> 0

highlight_show Body -> text_span { color "#00CC99"; render Body }


colorize_text T:text ->
// ----------------------------------------------------------------------------
//   Highlight or not depending on highlighting state
// ----------------------------------------------------------------------------
    if highlighting <> 0 then
        highlight_show T
    else
        text T


colorize_highlight T:text ->
// ----------------------------------------------------------------------------
//   Highlight names depending on highlight pattern
// ----------------------------------------------------------------------------
//   We must use a separate function to save the captured patten,
//   since it would be overwritten by the regexp_match we do here
    // writeln "Colorize [", T, "] h=", highlighting
    if regexp_match(T, highlight) then
        highlighting := 1
    else if regexp_match(T, highlight_start) then
        highlighting := -1
    else if regexp_match(T, highlight_end) then
        highlighting := 0
    if highlighting <> 0 then
        highlight_show T
    else
        text T
    if highlighting > 0 then
        highlighting := 0


colorize_type Colon:text, Type:text ->
// ----------------------------------------------------------------------------
//    Colorize a type annotation
// ----------------------------------------------------------------------------
    gray  { colorize_highlight Colon }
    green { colorize_highlight Type }


colorize Input:text ->
// ----------------------------------------------------------------------------
//   Render the given input using colorized text
// ----------------------------------------------------------------------------
    highlighting := 0
    regexp_parse_all Input,
        // Comments
        - "(// .*\n)|(/[*] .* [*]/)|(-- .*\n)|(#( .*)?\n)" ->
            red regexp_captured 0
        // Definition
        - "([^\n]*)\b(->|is)\b" ->
            italic { ltblue { colorize regexp_captured 1 } }
            gray regexp_captured 2
        // Real numbers (with base)
        "[0-9_]+#[0-9_A-Za-z]+[.][0-9_A-Za-z]+(#?[eE][+-]?[0-9_]+)?" ->
            ocean regexp_captured 0
        // Real numbers
        "[0-9_]+[.][0-9_]+([eE][+-]?[0-9_]+)?" ->
            ocean regexp_captured 0
        // Integer numbers (with base)
        "[0-9_]+#[0-9_A-Za-z]+([eE][+-]?[0-9_]+)?" ->
            ocean regexp_captured 0
        // Integer numbers (or real numbers if negative exponent)
        "[0-9_]+([eE][+-]?[0-9_]+)?" ->
            ocean regexp_captured 0
        // Text
        - "([""].*[""])|<<.*>>" ->
            dpurple regexp_captured 0
        // Characters
        - "'.*'" ->
            purple regexp_captured 0
        // Type declarations
        "((:|as)[ ]*)([A-Za-z][A-Za-z0-9_]*)" ->
            colorize_type regexp_captured 1, regexp_captured 3
        // Command-line tests
        "% [a-z][^\n]*\n" ->
            green regexp_captured 0
        // Names
        "[A-Za-z][A-Za-z_0-9]*" ->
            colorize_highlight regexp_captured 0
        // Symbols
        "[^A-Za-z0-9 ""'\n]+" ->
            navy { colorize_highlight regexp_captured 0 }
        // Spaces
        "[ \n]+" ->
            text regexp_captured 0
        // Other
        "." ->
            text regexp_captured 0
    refresh 0.1
    on "pageexit",
        highlight "none"


colorize_file Title:text, File:text ->
// ----------------------------------------------------------------------------
//   Colorize from a file
// ----------------------------------------------------------------------------
    code_box Title,
        colorize load_text File


colorize_file_at Time, Title:text, File:text ->
// ----------------------------------------------------------------------------
//   Colorize at a given time
// ----------------------------------------------------------------------------
    only_at Time,
        colorize_file Title, File


red     Body -> text_span { color "#AA0022"; render Body }
orange  Body -> text_span { color "#DD3311"; render Body }
yellow  Body -> text_span { color "#EEAA11"; render Body }
green   Body -> text_span { color "#006633"; render Body }
ltblue  Body -> text_span { color "#0066CC"; render Body }
blue    Body -> text_span { color "#003399"; render Body }
ocean   Body -> text_span { color "#229988"; render Body }
sky     Body -> text_span { color "#3399FF"; render Body }
navy    Body -> text_span { color "#002266"; render Body }
purple  Body -> text_span { color "#CC0099"; render Body }
dpurple Body -> text_span { color "#990066"; render Body }
gray    Body -> text_span { color "#777";    render Body }
code    Body -> text_span { font "Menlo";    blue   Body }
concept Body -> text_span { font "Amaze";    green  Body }
// ----------------------------------------------------------------------------
//   Draw something in red, green or blue
// ----------------------------------------------------------------------------


code_box X:real, Y:real, W:real, H:real, Title, Body ->
// ----------------------------------------------------------------------------
//    A text box showing a code example
// ----------------------------------------------------------------------------
    background -50,
        line_color "lightblue", 0.5
        linear_gradient 128, 0, 128, 256, 256, 256,
            gradient_color 0%, "lightgray", 80%
            gradient_color 100%, "white", 0%
        color "white"
        rectangle X, Y, W+50, H+50
    locally
        frame_texture W, H,
            text_box 0, 0, W, H,
                style "boxtitle"
                anchor
                    linear_gradient 32, 64, 32, 0, 64, 64,
                        gradient_color   0%, "#CCF", 70%
                        gradient_color  10%, "#448",  90%
                        gradient_color 100%, "#FFF", 0%
                    color "lightblue"
                    rectangle 0, -25, 5 * W, 65
                paragraph Title
                style "code"
                render Body
                vertical_align 10%
        color "white"
        rectangle X, Y, W, H


code_box Title, Body ->
// ----------------------------------------------------------------------------
//    Default location for the code box
// ----------------------------------------------------------------------------
    code_box code_x, code_y, code_w, code_h, Title, Body


code_x ->   0.0
code_y ->   0.0
code_w -> 700.0
code_h -> 800.0
code_size -> theme_size "XL-Lang", slide_master, "code"
code_size_adjust CS:integer, CV:integer -> CS := CV
code_size S:integer ->
    code_size_adjust code_size, S
    on "pageexit",
        code_size_adjust code_size, 32

code_position X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//    Move the code at the given location
// ----------------------------------------------------------------------------
    code_x := X
    code_y := Y
    code_w := W
    code_h := H


code_move_default ->
// ----------------------------------------------------------------------------
//   Restore to default position
// ----------------------------------------------------------------------------
    code_move 0, 0, 700, 800


code_move X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//    Move the code box smoothly
// ----------------------------------------------------------------------------
    interpolate 15%, X, code_x
    interpolate 15%, Y, code_y
    interpolate 15%, W, code_w
    interpolate 15%, H, code_h


code_box_at_step N, Title, Body ->
// ----------------------------------------------------------------------------
//   Draw a code box only at given step
// ----------------------------------------------------------------------------
    picture
        only_at N,
            code_box Title, Body


framed Color:text, Body ->
// ----------------------------------------------------------------------------
//    Consistent coloring for framed shapes
// ----------------------------------------------------------------------------
    locally
        radial_gradient 128, 85, 160, 256, 256,
            gradient_color 0%, Color, 20%
            gradient_color 100%, Color, 50%
        color "white"
        Body
    locally
        line_color Color, 80%
        color "tranparent"
        line_width 3
        Body


framed_box Color:text, X:real, Y:real, W:real, H:real, Body ->
// ----------------------------------------------------------------------------
//    A text box showing a code example
// ----------------------------------------------------------------------------
    background -50,
        framed Color, { rectangle X, Y, W, H }
    locally
        framed_box_offset -> 20
        frame_texture W-framed_box_offset, H-framed_box_offset,
            text_box 0, 0, W-20, H-20,
                style "story"
                vertical_align 30%
                align 50%
                render Body
        color "white"
        rectangle X, Y, W-framed_box_offset, H-framed_box_offset


image_box X:real, Y:real, W:real, H:real, Body ->
// ----------------------------------------------------------------------------
//    An image box with a cached frame texture
// ----------------------------------------------------------------------------
    background -50,
        line_color "lightblue", 0.5
        linear_gradient 128, 0, 128, 256, 256, 256,
            gradient_color 0%, "lightgray", 80%
            gradient_color 100%, "white", 0%
        color "white"
        rectangle X, Y, W+50, H+50
    locally
        frame_texture W, H,
            render Body
        color "white"
        rectangle X, Y, W, H


stamp Color, Body ->
// ----------------------------------------------------------------------------
//    Display a stamp with the given color
// ----------------------------------------------------------------------------
    locally
        rotate_z 30
        text_box -80, 250, 400, 80,
            font "Impact", 90
            color Color, sin(5 * page_time)^4
            render Body


image_only_at N, Body ->
// ----------------------------------------------------------------------------
//   Draw an image at the given step
// ----------------------------------------------------------------------------
    only_at N,
        locally
            color "white"
            Body


movie_only_at N, Name, Body ->
// ----------------------------------------------------------------------------
//   Draw a movie texture only at the given time
// ----------------------------------------------------------------------------
    image_only_at N,
        if smooth_step in N-0.9..N+0.9 then
            movie_texture Name
        else
            movie_drop Name
        on "pageexit",
            movie_drop Name
        Body
movie_only_at N, Name ->
    movie_only_at N, Name,
        rectangle texture_width, texture_height


full_screen_movie S:real, Name:text ->
// ----------------------------------------------------------------------------
//    Display a movie, using full screen if possible
// ----------------------------------------------------------------------------
    contents 50,
        color "black", S
        rectangle 32000, 32000
        color "white"
        movie_texture Name
        if texture_width > 0 and texture_height > 0 then
            scale min(real slide_width / texture_width, real slide_height / texture_height)
            scale 50% + 50 % * S
            rectangle texture_width, texture_height
        on "pageexit",
            movie_drop Name
        on "key:-",
            movie_set_time Name, movie_time Name - 10
        on "key:+",
            movie_set_time Name, movie_time Name + 10
        on "key:r",
            movie_set_time Name, 0
        on "key:p",
            full_screen_movie_pause := 1 - full_screen_movie_pause
            if full_screen_movie_pause = 1 then
                movie_pause Name
            else
                movie_play Name

full_screen_movie_pause -> 1

full_screen_movie Name:text ->
    full_screen_movie fade_at(smooth_step, 1), Name


images N:integer, X, Y ->
// ----------------------------------------------------------------------------
//   Display the images in a list one after the other
// ----------------------------------------------------------------------------
    images N, X
    images N+1, Y
images N:integer, "" -> false
images N:integer, Name:text ->
    image_only_at N,
        rotate_z 3 * sin (2 * N) ^ 5
        texture Name
        scale min(850.0 / texture_width, 640.0 / texture_height)
        rectangle texture_width, texture_height
images List -> images 1, List


colored_boxes N:integer, X, Y ->
// ----------------------------------------------------------------------------
//   Display the texts in imbricated colored boxes, one after the other
// ----------------------------------------------------------------------------
    colored_boxes N+1, Y
    colored_boxes N, X
colored_boxes N:integer, X ->
    locally
        at_step N
        locally
            color_hsv 37 * N, 50%, 50%, 80%
            line_color_hsv 37 * N, 70%, 80%, 100%
            line_width 3
            rectangle 0, 30 * N, 800 + 50 * N, 80 * N
        text_box 0, 30 * N, 800 + 50 * N, 80 * N,
            style "story"
            align 50%
            vertical_align 0%
            color "white"
            render X
colored_boxes List ->
    translate_y -200
    colored_boxes 1, List


structure List ->
// ----------------------------------------------------------------------------
//    Draw a data structure
// ----------------------------------------------------------------------------
    locally
        translate_y 200
        align 50%
        vertical_align 30%
        structure_boxes 0, List

structure_boxes N:integer, Head, Tail ->
    structure_box N, Head
    translate_y -70
    structure_boxes N+1, Tail
structure_boxes N:integer, Head ->
    structure_box N, Head

structure_box N:integer, Body ->
    locally
        line_color_hsv 37 * N, 40%, 60%
        color_hsv 20 * N, 40%, 30%
        line_width 3
        rectangle 520, 65
    text_box 0, 0, 310, 30,
        color "white"
        font "Overpass", "Arial", 30, bold
        render Body

sequence List ->
// ----------------------------------------------------------------------------
//    Draw a sequence of elements
// ----------------------------------------------------------------------------
    locally
        translate_y 200
        align 50%
        vertical_align 30%
        sequence_boxes 0, List

sequence_boxes N:integer, Head, Tail ->
    sequence_box N, Head
    locally
        color "lightyellow"
        line_color "darkorange"
        line_width 3
        rotate_z -90
        translate_z 10
        arrow 60, 0, 70, 60, 50, 60%
    translate_y -120
    sequence_boxes N+1, Tail
sequence_boxes N:integer, Head ->
    sequence_box N, Head

sequence_box N:integer, Body ->
    locally
        line_color_hsv 37 * N, 40%, 60%
        color_hsv 37 * N, 40%, 30%
        line_width 3
        rectangle 520, 65
    text_box 0, 0, 510, 30,
        color "white"
        font "Overpass", "Arial", 30, bold
        render Body


quotation_box W, H, Text, Author ->
// ----------------------------------------------------------------------------
//   Draw a box to display some quotation
// ----------------------------------------------------------------------------
   locally
        color "black"
        rectangle W + 30, H + 30
        line_color "white"
        line_width 5
        rectangle W + 15, H + 15
   text_box 0, 0, W, H,
       style "quote"
       italic
       paragraph Text
       roman
       align 80%
       paragraph Author


math_eq Eq:text ->
// ----------------------------------------------------------------------------
//   Small utility to draw an equation
// ----------------------------------------------------------------------------
    image "http://latex.codecogs.com/png.latex?\dpi{600}\color{White}" & Eq



// ============================================================================
//
//    Taodyne colors
//
// ============================================================================

// Taodyne logo colors
tao "red"                               -> "#AA0022"
tao "orange"                            -> "#DD3311"
tao "yellow"                            -> "#EEAA11"
tao "green"                             -> "#006633"
tao "ocean"                             -> "#229988"
tao "sky"                               -> "#3399FF"
tao "navy"                              -> "#003388"
tao "purple"                            -> "#CC0099"
tao "grey"                              -> "#555"



// ============================================================================
//
//   Timer
//
// ============================================================================

TOTAL_DURATION -> 1200 // 20 minutes
START_TIME     -> 0.0

key "Right" -> skip 1
key "Space" -> skip 1
key "Left"  -> skip -1
key "1" -> skip_directly_to 1 ; refresh 0.1
key "2" -> skip_directly_to 2 ; refresh 0.1
key "3" -> skip_directly_to 3 ; refresh 0.1
key "4" -> skip_directly_to 4 ; refresh 0.1
key "5" -> skip_directly_to 5 ; refresh 0.1
key "6" -> skip_directly_to 6 ; refresh 0.1
key "7" -> skip_directly_to 7 ; refresh 0.1
key "8" -> skip_directly_to 8 ; refresh 0.1
key "9" -> skip_directly_to 9 ; refresh 0.1
key "0" -> skip_directly_to 10; refresh 0.1

seconds_time -> hours * 3600 + minutes * 60 + seconds
key "Backspace" ->
    START_TIME := seconds_time
    goto_page 1
once
    START_TIME := seconds_time

contents 0,
    color "lightgray"
    rectangle 0, -482, 1700 * (seconds_time - START_TIME) / TOTAL_DURATION, 2
    if page_count > 0 then
        rectangle 0, -478, 1700 * page_number / page_count, 2


// ============================================================================
//
//   Virtual laser pointer (activate with "l")
//
// ============================================================================

key "l" -> laser := 1 - laser
key "L" -> show_coordinates := 1 -  show_coordinates
laser -> 0
show_coordinates -> 0
contents 500,
    if show_coordinates > 0 then
        text_box mouse_x + 90, mouse_y + 40, 150, 90,
            color "red", 80%
            font "Arial", 35, bold
            paragraph "X=" & integer mouse_x
            paragraph "Y=" & integer mouse_y

locally
    translate_z 500
    color "red", 80% * laser
    rectangle mouse_x, 0, 1, 10000
    rectangle 0, mouse_y, 10000, 1
    circle mouse_x, mouse_y, 3
    line_color "red", 80% * laser
    color "red", 10% * laser
    circle mouse_x, mouse_y, 10


// ============================================================================
//
//    Shader display
//
// ============================================================================

viewpoint_delta_base    -> 0.01
viewpoint_delta         -> viewpoint_delta_base * eye_distance

shader_x                -> 0.0
shader_y                -> 0.0
shader_scale            -> 1.0
shader_scale S          -> shader_scale := S
shader_rscale           -> 1.0
shader_rscale S         -> shader_rscale := S
shader_rrate            -> 60.0
shader_autoscale        -> 0

shader_show File, Arguments ->
// ----------------------------------------------------------------------------
//   Display a shader with the given file name
// ----------------------------------------------------------------------------
  locally
    if page_time < 0.1 then shader_speed 1
    shader_rscale := shader_scale / sqrt(stereo_viewpoints)
    WIDTH  := window_width  * shader_rscale
    HEIGHT := window_height * shader_rscale
    color "white"
    shader_x := mouse_x
    shader_y := mouse_y

    if shader_autoscale > 0 then
        nonzero 0.0 -> 1e5
        nonzero X -> X
        shader_rrate := 0.95 * shader_rrate + 0.05 / nonzero refresh_time
        if shader_rrate < 15 then
            shader_scale := shader_scale * 0.99
        else if shader_rrate >= 20 and shader_scale < 1 then
            shader_scale := shader_scale * 1.001

    for I in 0..stereo_viewpoints-1 loop
        stereo_viewpoints 2^I,
            frame_texture WIDTH, HEIGHT,
                clear_color 0, 0, 0, 1
                shader_program
                    fragment_shader_file File
                Arguments
                shader_set iViewpoint := I * viewpoint_delta
                color "white"
                rectangle WIDTH, HEIGHT
            color "white"
            rectangle 0, 120, slide_width, slide_height


    if shader_autoscale > 0 then
        contents 0,
            color "darkgreen", 0.5
            ellipse_sector -800, -400, 180, 180, 180, -180 * shader_scale
            color "darkred", 0.5
            ellipse_sector -800, -400, 200, 200, 180, -180 * shader_rrate / 60
    on "key:r",
        shader_scale := 1.0
    on "key:a",
        shader_autoscale := 1 - shader_autoscale


shader_speed       -> 1.0
shader_speed S     -> shader_speed := S
shader_resolution  -> shader_set iResolution := WIDTH, HEIGHT, 1
shader_time        -> shader_set iGlobalTime := page_time * shader_speed
shader_gtime       -> shader_set iGlobalTime := (time+7200) mod 86400 * shader_speed
shader_atime       -> shader_set iGlobalTime := time mod 813 * shader_speed
shader_htime       -> shader_set iGlobalTime := time mod 997 * shader_speed
shader_mouse       -> shader_set iMouse := shader_x+slide_width/2, shader_y+slide_height/2, mouse_buttons and 1, mouse_buttons and 2
shader_depth X:real -> viewpoint_delta_base := 0.01 * X
shader_channel 0,T -> shader_chantex 0, T; shader_set iChannel0 := 0
shader_channel 1,T -> shader_chantex 1, T; shader_set iChannel1 := 1
shader_channel 2,T -> shader_chantex 2, T; shader_set iChannel2 := 2
shader_channel 3,T -> shader_chantex 3, T; shader_set iChannel3 := 3
shader_chanres 0 ->
    shader_set iChannelResolution0 := 1.0/texture_width, 1.0/texture_height, 1
shader_chanres 1 ->
    shader_set iChannelResolution1 := 1.0/texture_width, 1.0/texture_height, 1
shader_chanres 2 ->
    shader_set iChannelResolution2 := 1.0/texture_width, 1.0/texture_height, 1
shader_chanres 3 ->
    shader_set iChannelResolution3 := 1.0/texture_width, 1.0/texture_height, 1
shader_chantex N,T ->
    texture_unit N
    if T contains ".mp4" or T contains "youtube" then
        movie_texture T
        on "pageexit",
            movie_drop T
        texture_wrap true, true
    else if T contains "#" then
        cube_map 512,
            cube_map_flip false, false
            cube_map_face 0, text_replace(T, "#", "1")
            cube_map_face 1, text_replace(T, "#", "2")
            cube_map_face 2, text_replace(T, "#", "3")
            cube_map_face 3, text_replace(T, "#", "4")
            cube_map_face 4, text_replace(T, "#", "5")
            cube_map_face 5, text_replace(T, "#", "6")
    else if T contains "cross" then
        cube_map_cross T
    else
        texture T
        texture_wrap true, true
shader_date     -> shader_set iDate := year, month, day, time mod 86400



// ============================================================================
//
//    Display artist information
//
// ============================================================================

description T ->
// ----------------------------------------------------------------------------
//   Display information about the artist
// ----------------------------------------------------------------------------
    locally
        show fade_out_at(page_time, 5)
        texture ""
        translate 0, -100, 200
        rotate_y -30
        light 0
        light_position 100, 100, 1000
        text_box 0, 0, 1600, 200,
            style "story"
            font "Cherry Cream Soda", bold, 60
            extrude_depth 25
            align 1
            vertical_align 1
            text T
    locally
        show_news -> 0
        if show_news > 0 then
            rtn_newsfeed
        on "key:i",
            show_news := 1 - show_news

artist T -> description T



// ============================================================================
//
//    Per-display size adjustments
//
// ============================================================================

WIDTH -> 1920.0
HEIGHT -> 1080.0

adjust_width "alioscopy" ->
    WIDTH := window_width*0.4
    HEIGHT := window_height*0.4
adjust_width "united_entertain" ->
    WIDTH := window_width*0.4
    HEIGHT := window_height*0.4
adjust_width "tridelity_ml" ->
    WIDTH := window_width*0.4
    HEIGHT := window_height*0.4
adjust_width "tridelity_mv" ->
    WIDTH := window_width*0.4
    HEIGHT := window_height*0.4
adjust_width "tridelity_mp" ->
    WIDTH := window_width*0.4
    HEIGHT := window_height*0.4
adjust_width "hsplit" ->
    WIDTH := window_width*0.2
    HEIGHT := window_width*0.4
adjust_width "vsplit" ->
    WIDTH := window_width*0.4
    HEIGHT := window_width*0.2
adjust_width Other ->
    WIDTH := window_width
    HEIGHT := window_height



// ============================================================================
//
//   Quoting utility
//
// ============================================================================

quote_step              -> 0.0
quote_step S            -> quote_step := S
quote_smooth_step       -> 0.0
quote_index             -> 0
quote_count             -> 1
quote_cosR              -> 0.0


quotes Body     ->
// ----------------------------------------------------------------------------
//    Present a list of quotes
// ----------------------------------------------------------------------------
    picture
        quote_index := 0
        interpolate 10%, quote_step, quote_smooth_step
        font "Hoefler Text", italic, 60
        align 50%
        vertical_align 30%
        Body
        quote_count := quote_index


quote_setup R:real -> quote_setup cos R, sin R
quote_setup cosR:real, sinR:real ->
// ----------------------------------------------------------------------------
//   Setup the location for a quote
// ----------------------------------------------------------------------------
    translate 0, -300 * sinR, 2500 * cosR - 2200
    color_hsv 170 + 31 * quote_index, cosR * 0.5, 60%
    show 5% + ((1+cosR)/2)^8
    quote_cosR := cosR


quote Author, Texts ->
// ----------------------------------------------------------------------------
//   Show a given quote
// ----------------------------------------------------------------------------
    locally
        quote_setup (quote_index - quote_smooth_step) * 2 * pi / quote_count
        if quote_cosR > 0.9 then
            locally
                translate_z -30
                color "white", 90%
                line_color "darkblue", (quote_cosR - 0.9) * 10
                line_width 2
                rounded_rectangle 0, 0, 1400, 550, 20
        text_box 0, 0, 1150, 500,
            quote_render Texts
            text_span { font_size 12; paragraph " " }
            roman
            align_right
            paragraph Author
        quote_image Author
    quote_index := quote_index + 1

quote_render Text, Texts ->
    quote_render Text
    quote_render Texts
quote_render Text ->
    render Text
    line_break


quote_image Author:text ->
// ----------------------------------------------------------------------------
//   Show the picture for a given author
// ----------------------------------------------------------------------------
    locally
        translate -670, 280, 15
        color "white"
        texture Author & ".jpg"
        ellipse 0, 0, 260, 320

quote_image Other -> false



// ============================================================================
//
//   Diagramming
//
// ============================================================================

soft_box X:real, Y:real, Color:text, Body ->
// ----------------------------------------------------------------------------
//   Soft box with preset width and height
// ----------------------------------------------------------------------------
    soft_box X, Y, 400, 200, Color, Body


soft_box X:real, Y:real, W:real, H:real, Color:text, Body ->
// ----------------------------------------------------------------------------
//   Draw a software box
// ----------------------------------------------------------------------------
    color Color
    rounded_rectangle X, Y, W, H, 40
    locally
       translate_z 2
       text_box X, Y, W-5, H-5,
           style "caption-white"
           render Body


anim_step               -> 0
anim_smooth_step        -> 0.0
anim_step_selected      -> 0.0
anim_step Step:integer  ->
// ----------------------------------------------------------------------------
//   Change the animation step
// ----------------------------------------------------------------------------
    if Step <> anim_step then
        anim_step := Step
        refresh 0.1
    interpolate 0.1, anim_step, anim_smooth_step


anim X:real -> X
anim X:real, Values ->
// ----------------------------------------------------------------------------
//    Compute animated value
// ----------------------------------------------------------------------------
    anim_step_selected := X
    anim_select X, Values
    if X in anim_step_selected-0.01..anim_step_selected+0.01 then
        X := anim_step_selected
    else
        interpolate 10%, anim_step_selected, X
    X + 0.0


anim_select X:real, S:integer = TX:real ->
// ----------------------------------------------------------------------------
//   Compute an animated value that sets X to TX at step S
// ----------------------------------------------------------------------------
    if anim_step >= S then
        anim_step_selected := TX
    X + 0.0


anim_select X:real, S:integer = TX:real, Rest ->
// ----------------------------------------------------------------------------
//   Compute animated value in a list
// ----------------------------------------------------------------------------
    anim_select X, S = TX
    anim_select X, Rest


anim_text X:text, S:integer = TX:text ->
// ----------------------------------------------------------------------------
//   Compute an animated value that sets X to TX at step S
// ----------------------------------------------------------------------------
    if anim_step >= S then
        TX
    else
        X


anim_text X:text, S:integer = TX:text, Rest ->
// ----------------------------------------------------------------------------
//   Compute animated value in a list
// ----------------------------------------------------------------------------
    anim_text anim_text(X, S = TX), Rest


at_anim Step:real ->
// ----------------------------------------------------------------------------
//   Show at the given animation step
// ----------------------------------------------------------------------------
    show fade_at(anim_smooth_step, Step)


only_at_anim N:integer..M:integer, Body ->
// ----------------------------------------------------------------------------
//   Show something at step N only
// ----------------------------------------------------------------------------
    if anim_smooth_step in N-1..M+1 then
        locally
            show 1.3*fade_between(anim_smooth_step, N, M)
            Body


only_at_anim N:integer, Body ->
// ----------------------------------------------------------------------------
//   Show something at step N only
// ----------------------------------------------------------------------------
    if anim_smooth_step in N-1..N+1 then
        locally
            show 1.3*fade_between(anim_smooth_step, N, N)
            Body


check_refresh ->
// ----------------------------------------------------------------------------
//   Check if we have overly-eager refresh
// ----------------------------------------------------------------------------
    counter -> 0
    writeln "Refresh ", counter, " step=", smooth_step
    counter := counter + 1



// ============================================================================
//
//   Stacked items
//
// ============================================================================

stacked_item_index -> 0
stacked_item_count -> 1
stacked_item_unit  -> ""
stacked_item_total -> 0.0
stacked_item_sum   -> 0.0
stacked_item_scale -> 1.0

STACK_HEIGHT -> 600.0
DESCR_HEIGHT -> 60


stacked_scaled Height:real, Value:real, Caption:text, Description:text ->
    color_hsv 37.311 * stacked_item_index, 90%, 80%
    rounded_rectangle -650, Height/2, 100, Value * stacked_item_scale, 10
    translate_y Height + 1
    stacked_item_total := stacked_item_total + Height + 1
    stacked_item_sum := stacked_item_sum + Value
    locally
        show 30%
        path
            move_to -600, -1
            line_to -600, -(Height + 1)
            line_to -420, (STACK_HEIGHT - DESCR_HEIGHT * (stacked_item_count-1)) / 2 - stacked_item_total + stacked_item_index * DESCR_HEIGHT - DESCR_HEIGHT/2
            line_to -420, (STACK_HEIGHT - DESCR_HEIGHT * (stacked_item_count-1)) / 2 - stacked_item_total + stacked_item_index * DESCR_HEIGHT + DESCR_HEIGHT/2

    locally
        translate_y (STACK_HEIGHT - DESCR_HEIGHT * (stacked_item_count-1)) / 2 - stacked_item_total - Height/2 + stacked_item_index * DESCR_HEIGHT
        text_box -580, Height/2, 300, Height,
            style "story"
            color "#777"
            align 1
            vertical_align 0.5
            font_size 26
            render Value
            render stacked_item_unit
        rounded_rectangle -300, Height/2, 230, DESCR_HEIGHT - 2, 10
        text_box -300, Height/2, 250, DESCR_HEIGHT,
            style "story"
            color "white"
            font_size 30
            align 0.5
            vertical_align 0.4
            text Caption
        text_box  450, Height/2, 1200, Height,
            style "story"
            align 0
            vertical_align 0.5
            text Description

stacked_round X:real -> 0.1 * integer(X * 10)

stacked Height:real, Caption:text, Description:text ->
    if Height > 0.0 then
        stacked_scaled Height * stacked_item_scale, stacked_round(Height), Caption, Description
    stacked_item_index := stacked_item_index + 1


reference_scale Height:real, Caption ->
    locally
        line_color redhat_blue
        line_width 6
        line_arrow -730, 0, none, -730, Height * stacked_item_scale, arrowhead
    text_box -850, Height * stacked_item_scale - 40, 200, 80,
        style "story"
        color redhat_blue
        align 1
        render Caption

reference_tick Height:real, Caption ->
    locally
        line_color redhat_blue
        line_width 2
        line_arrow -740, Height * stacked_item_scale, none, -720, Height * stacked_item_scale, none
    text_box -850, Height * stacked_item_scale, 200, 80,
         style "story"
         color redhat_blue
         vertical_align 0.5
         align 100%
         render Caption


stacked_items Unit:text, Scale:real, Body ->
    locally
        stacked_item_index := 0
        stacked_item_unit := Unit
        stacked_item_scale := Scale
        stacked_item_total := 0.0
        stacked_item_sum := 0.0
        translate 0, -300, 0

        Body

        stacked_item_count := stacked_item_index


last_graph_index -> 0.0
last_graph S:integer, Information ->
    if last_graph_index <> S then
        last_graph_index := S
        refresh 0.0
    else
        locally
            at_step S
            text_box 650, 250, 500, 600,
                style "story"
                render Information

bar_graph_unit   -> ""
bar_graph_unit U -> bar_graph_unit := U


bar_graph Step:integer, Value1:real, Value2:real, Description ->
    locally
        at_step Step
        color_hsv 37.311 * Step, 90%, 60%
        translate Step * 100 - 50 * last_graph_index - 100, 0, 0
        bar_graph_draw Value1 * fade_at(smooth_step, Step) * stacked_item_scale
        locally
            color_hsv 37.311 * Step, 90%, 80%
            translate_y Value1 * fade_at(smooth_step, Step) * stacked_item_scale
            bar_graph_draw (Value2 - Value1) * fade_at(smooth_step, Step) * stacked_item_scale

        if stacked_item_sum < Value2 * fade_at(smooth_step, Step) then
            stacked_item_sum := Value2 * fade_at(smooth_step, Step)

        style "story"
        align 100%
        vertical_align 30%
        color_hsv 37.311 * Step, 90%, 40%
        font_size 20

        text_box 0, Value1 * fade_at(smooth_step, Step) * stacked_item_scale + 50, 100, 100,
            align 50%
            vertical_align 100%
            color "white"
            text "" & integer(Value1 * 10) * 0.1 & bar_graph_unit

        text_box 0, Value2 * fade_at(smooth_step, Step) * stacked_item_scale + 50, 100, 100,
            align 50%
            vertical_align 100%
            text "" & integer(Value2 * 10) * 0.1 & bar_graph_unit

        translate -70, -100, 0
        rotate_z 40
        text_box 0, 0, 200, 80,
            font_size 26
            render Description

bar_graph Step:integer, Value:real, Description ->
    locally
        at_step Step
        color_hsv 37.311 * Step, 90%, 80%
        translate Step * 100 - 50 * last_graph_index - 100, 0, 0
        bar_graph_draw Value * fade_at(smooth_step, Step) * stacked_item_scale
        if stacked_item_sum < Value * fade_at(smooth_step, Step) then
            stacked_item_sum := Value * fade_at(smooth_step, Step)

        style "story"
        align 100%
        vertical_align 30%
        color_hsv 37.311 * Step, 90%, 40%
        font_size 20

        text_box 0, Value * fade_at(smooth_step, Step) * stacked_item_scale + 50, 100, 100,
            align 50%
            vertical_align 100%
            text "" & integer(Value * 10) * 0.1 & bar_graph_unit

        translate -70, -100, 0
        rotate_z 40
        text_box 0, 0, 200, 80,
            font_size 26
            render Description

bar_graph_draw Value:real ->
    rounded_rectangle 0, 0.5 * Value, 90, Value, 10



// ============================================================================
//
//    Multilingual support
//
// ============================================================================

LANG -> "en"
English fr French -> if LANG="fr" then French else English
english Body -> if LANG="en" then Body
french  Body -> if LANG="fr" then Body
key "f" -> LANG := "fr"; refresh 0
key "e" -> LANG := "en"; refresh 0



// ============================================================================
//
//   Image snapshots
//
// ============================================================================

key "t" -> snapshot timestamp

snapshot Time:text ->
    screenshot "snapshots/Snapshot-" & timestamp & ".png"
    writeln "![Snapshot-", timestamp, ".png](snapshots/Snapshot-",timestamp,".png)"
