presentation_title "Confidential Containers Workshop - DevConf.cz 2023" fr "Les conteneurs sécurisés - Ne faites confiance à personne!"
presentation_logo "ConfidentialContainersLogo.png"
presentation_url "https://github.com/c3d/presentations"
presentation_tao "https://github.com/c3d/presentations (branch devconfcz-2023-workshop)" fr "https://github.com/c3d/presentations (branche devconfcz-2023-workshop)"
presentation_author "Christophe de Dinechin"
presentation_author_title "Senior Principal Software Engineer"
presentation_author_mail "dinechin@redhat.com"



// ----------------------------------------------------------------------------
main_title_slide "Main title",
// ----------------------------------------------------------------------------
    title
        $ "Confidential Containers Primer" fr "Introduction aux conteneurs sécurisés"
    subtitle
        $ "DevConf.cz 2023" fr "DevConf.cz 2023"

    title_slide_author "Christophe.jpg",
        $ presentation_author
        $ { italic presentation_author_title }
        font "Overpass Mono"
        $ presentation_author_mail

    title_slide_logo


disabled
    my_picture_page with files "Trucs/*"

my_picture_page Name ->
    base_slide Name,
        picture
            if Name contains ".mov" then
                movie_texture Name
                on "pageexit",
                    movie_drop Name
            else
                texture Name
            if texture_width > 0 and texture_height > 0 then
                scale min(real slide_width/texture_width, real slide_height/texture_height)
            else
                writeln "No texture for ", Name
            rectangle 0,160,texture_width, texture_height


// ---------------------------------------------------------------------------
base_slide "Agenda",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Key topics we are going to cover today" fr "Les sujets du jour"
    slide_picture "Agenda.jpg", 0%, 320 // https://www.pexels.com/fr-fr/photo/programme-startup-a-faire-concept-7376/
    stepping
    story
        * "Quick primer on confidential containers"
        ** "What is the Confidential Containers project"
        ** "Vendor landscape"
        ** "Ancestry: Kata Containers"
        * "Development phases"
        ** "Hardware enablement"
        ** "Securing image pull"
        ** "Attestation"

    right_picture
        images "Nosy Neighbour.jpg", "ConfidentialContainersProject.png", "Zombie Apocalypse.jpg", "KataContainersLogo.png"


// ============================================================================
//
section_slide "Confidential Containers",
//
// ============================================================================
    title page_label
    subtitle
        $ "A quick primer"
    stepping
    page_quote 0


// ----------------------------------------------------------------------------
base_slide "A very active project" fr "Un projet très actif",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "https://github.com/confidential-containers"
    slide_picture "ConfidentialContainersQRCode.png", 0, 300
    stepping
    contents -200,
        color "white"
        translate_y mouse_y
        image 0, 0, 50%, 50%, "ConfidentialContainersProject.png"
    contents -100,
        color "white", 0.8
        rectangle 0, 400, 2000, 220
        rectangle 0, -510, 2000, 80


// ----------------------------------------------------------------------------
base_slide "Confidential Computing" fr "L'informatique sécurisée",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Why should infrastructure see your data?" fr "Votre hébergeur peut-il voir vos données?"
    slide_picture "Confidential Computing.jpg", 5%, 350
    stepping

    picture
        caption_at CNF_HOST, "Software now runs on hardware you do not own, like a cloud provider" fr "Votre logiciel tourne maintenant sur l'ordi de quelqu'un d'autre"
        caption_at CNF_RESOURCES, "Hardware resources are owned by the host" fr "Les resources matérielles appartiennent en réalité à l'hébergeur qui les loue"
        caption_at CNF_CONTAINERS, "Containers carve out resources from the host" fr "Le conteneurs empruntent les resources dont ils ont besoin à cet hébergeur"
        caption_at CNF_SANDBOX_OUT, "Classical sandboxing only protects the host from the containers running on it" fr "L'isolation classique est conçue pour protéger l'hébergeur des conteneurs"
        caption_at CNF_SANDBOX_IN, "The host can freely peek insdie the container, for example read its memory" fr "À l'inverse, l'hébergeur peut tout à fait regarder dans un conteneur"
        caption_at CNF_TENANTS, "For that reason, multiple tenants do not want to share hardware when processing sensitive data" fr "Des concurrents ne voudront pas partager le même matériel pour des données sensibles"
        caption_at CNF_SAFE_TODAY, "Data on disk or in network is already encrypted today, so the VM host cannot read it nor tamper with it" fr "Les données sur disque ou réseau sont déjà chiffrées aujourd'hui, donc OK"
        caption_at CNF_MEMORY_VM, "In non-CC architectures, data in memory is not encrypted, so it can be accessed by the host" fr "Pour les données en mémoire vive, il faut le nouveau matériel"
        caption_at CNF_MEMORY_HOST, "Encrypted memory stores live data as cypher text, so that it becomes garbled when read by the host" fr "Les données ainsi protégées deviennent illisibles pour l'hébergeur"
        caption_at CNF_INTEGRITY, "Integrity ensures the host cannot corrupt nor poison CPU state or RAM contents" fr "Il faut aussi protéger l'intégrité de l'état du CPU ou de la mémoire"
        caption_at CNF_ATTESTATION, "Attestation proves where you are running and what you are running" fr "L'attestation permet de contrôler ce qui tourne, mais aussi où ça tourne"
        confidential integer(smooth_step + 0.5)


// ----------------------------------------------------------------------------
base_slide "Vendor landscape" fr "Les vendeurs et leur offre",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Different vendors with different approaches?" fr "Chaque vendeur a son approche bien à lui"
    slide_picture "Confidential Computing.jpg", 5%, 350
    stepping
    story
        * "AMD: Secure Encrypted Virtualization (SEV)"
        ** "SEV-ES adds Encrypted State (e.g. CPU register file)" fr "SEV-ES rajoute une protection de l'état CPU"
        ** "SEV-SNP adds Secure Nested Pages (integrity protection)" fr "SEV-SNP ajoute une notion d'intégrité"
        * "Intel: Trusted Domain Extensions (TDX)"
        * "IBM S390: Secure Execution (SE)"
        * "Power: Protected Execution Facility (PEF)"
        * "Arm: Confidential Computing Architecture (CCA)"
        * "All these technologies are based on virtualization" fr "Ces technologies sont toutes très virtuelles"
        * "They all work differently" fr "Elles fonctionnent toutes de façon différente"

    right_picture
        images "AMD SEV.png", "SEV-ES.png", "SEV-SNP.jpg", "Intel TDX.png", "S390 SE.jpg", "Power PEF.png", "Arm CCA.png", "Confidential VMs.jpg", "Zombie Apocalypse.jpg"

    right_picture
        entry_time -> 0.0
        if smooth_step < 9 then
            entry_time := page_time
        only_at 9,
            jump_factor -> fade_at(page_time - entry_time, 5)
            show sin(2.5 * time)^2
            translate 100 - 500 * jump_factor, 100 - 100 * fade_out(page_time, 10), 50 + 600 * jump_factor
            scale 1 + 5 * jump_factor + 0.3 * jump_factor * sin(45.3 * time)
            rotate_z 23 * sin((0.21 + 2.1 * jump_factor) * time) * (1.0 - 0.7 * jump_factor) + sin(30.2 * time) * jump_factor
            style "boxtitle"
            color_hsv 20 * time, 1, 1
            line_color "green"
            text_box 0, 0, 300, 200,
                vertical_align 0.5
                align 0.5
                $ "There be zombies!" fr "Des zombies partout!"


// Position of the "Talk box"
TALK_X          -> anim   650.0
TALK_Y          -> anim  -70.0, KC_CONFIDENTIAL=-180
TALK_W          -> anim  500.0
TALK_H          -> anim  600.0, KC_CONFIDENTIAL= 540
TALK_O          -> anim   50.0

talk_frame Body ->
// ----------------------------------------------------------------------------
//   Display the "talk" frame
// ----------------------------------------------------------------------------
    locally
        line_color "black"
        line_width 2
        color "white", 0.8
        rectangle TALK_X, TALK_Y, TALK_W + TALK_O, TALK_H + TALK_O
    text_box TALK_X, TALK_Y, TALK_W, TALK_H,
        style "story"
        vertical_align 30%
        font_size 32
        Body


// Adjustment to the main diagram
DIAGRAM_Scale   -> anim  1.0, 0=1, KC_CTR_ECOSYS=  75%, KC_CONFIDENTIAL=100%, KC_LINUX_KERNEL= 80%, KC_RELYING=  75%, KC_MSMTS= 100%
DIAGRAM_X       -> anim  0.0, 0=0, KC_CTR_ECOSYS=-300,  KC_CONFIDENTIAL=0,    KC_LINUX_KERNEL=  0,  KC_RELYING=-280,  KC_MSMTS= -800
DIAGRAM_Y       -> anim  0.0, 0=0,                      KC_CONFIDENTIAL=0,    KC_LINUX_KERNEL=110,  KC_RELYING= 100,  KC_MSMTS=    0

adjust_diagram ->
// ----------------------------------------------------------------------------
//   Adjust the position of the diagram
// ----------------------------------------------------------------------------
    // writeln "Step=", anim_step, " Scale=", DIAGRAM_Scale, " X=", DIAGRAM_X, " Y=", DIAGRAM_Y
    translate DIAGRAM_X, DIAGRAM_Y, 0
    scale DIAGRAM_Scale


// ----------------------------------------------------------------------------
base_slide "Kata Containers Overview" fr "La base: Kata Containers",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Run containers in virtual machines" fr "Des conteneurs dans des machines virtuelles"
    slide_picture "KataContainersLogo.png", 5%, 350
    stepping

    kata_containers integer(smooth_step + 0.5)
    contents 0,
        at_step KC_CTR_ECOSYS
        talk_frame
            font_size 42
            color "#14E"
            $ "Containers ecosystem" fr "L'écosystème des conteneurs"
            at_step KC_VM_ISOL
            color "#184"
            $ "Virtual machine isolation" fr "L'isolation des machines virtuelles"



// ----------------------------------------------------------------------------
base_slide "Confidential Containers in Kata" fr "Conteneurs sécurisés avec Kata",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Taking advantage of confidential computing" fr "Application de l'informatique sécurisée"
    slide_picture "Confidential - Top Secret.png", 5%, 300
    stepping

    kata_containers integer(KC_CONFIDENTIAL + smooth_step + 0.5)
    contents 0,
        at_step CC_PLATFORM
        talk_frame
            color "#C22"
            rectangle 0, 7, 50, 20
            bold " Trusted platform: " fr " Plateforme sécurisée: "
            color "black"
            $ "Offers confidentiality guarantees enforced by hardware cryptography" fr "Offre des garanties de sécurité à l'aide de cryptographie matérielle"
            at_step CC_HOST
            color "#24C"
            rectangle 0, 7, 50, 20
            bold " Host: " fr " Hébergeur: "
            color "black"
            $ "Manages and offers resources used to run containers (CPU, memory, I/O, etc)" fr "Gère et présente les resources pour les conteneurs (CPU, mémoire, E/S, etc)"
            at_step CC_TENANT
            color "#2A2"
            rectangle 0, 7, 50, 20
            bold " Tenant: " fr " Locataire: "
            color "black"
            $ "Confidential area, inaccessible to the host even when running on it" fr "Environnement confidentiel, inaccessible à l'hébergeur même lorsqu'elle l'utilise"


// ----------------------------------------------------------------------------
base_slide "Phase 1: Hardware enablement" fr "Phase 1: Activation matérielle",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Just activating the confidential computing technologies" fr "Mise en place des technologies sécurisées"
    slide_picture "Confidential - Top Secret.png", 3%, 300
    stepping

    kata_containers integer(KC_ENABLEMENT + smooth_step + 0.5)
    contents 0,
        at_step 1
        talk_frame
            font_size 32 - 6 * fade_at(smooth_step, HE_RUNTIME_CC)
            at_step HE_HARDWARE
            bold "Hardware: " fr "Matériel: "
            $ "Encryption support in memory controller" fr "Support du chiffrage dans le contrôleur de mémoire"
            at_step HE_FIRMWARE_CC
            bold "Firmware: " fr "Microgiciel: "
            $ "Special services like physical page validation" fr "Services spéciaux comme la validation de pages"
            at_step HE_KERNEL
            bold "Kernel: " fr "Noyau: "
            $ "Low-level hardware support, e.g. SEV, TDX" fr "Support bas niveau, p.ex. SEV, TDX"
            at_step HE_RUNTIME_CC
            bold "Runtime: " fr "Runtime: "
            $ "Pass the right options to hypervisor" fr "Passe des options à l'hyperviseur"
            at_step HE_HYPER_CC
            bold "Hypervisor: " fr "Hyperviseur: "
            $ "Setup virtual machines with encryption, etc" fr "Machines virtuelles avec chiffrage mémoire"
            at_step HE_AGENTCC
            bold "Agent: " fr "Agent: "
            $ "Image download, confidential services" fr "Téléchargement d'images, services confidentiels"


        at_step HE_COMPLETE
        stamp "green", "Complete!" fr "Terminé!"


// ----------------------------------------------------------------------------
base_slide "Phase 2: Securing image pull" fr "Phase 2: sécuriser le téléchargement",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Download images from within the guest" fr "Télécharger les images depuis la machine virtuelle"
    slide_picture "Secure Cloud.png", 3%, 300
    stepping

    kata_containers integer(KC_PULLIMAGE + smooth_step + 0.5)
    contents 0,
        at_step 1
        talk_frame
            at_step PI_INSIDE_GUEST
            bold "Pull Image " fr "Téléchargement d'images "
            $ "from inside the guest instead of from the host" fr "depuis la machine virtuelle et non depuis la machine physique hôte"
            at_step PI_ENCRYPTED
            bold "Store Images " fr "Stockage les images "
            $ "on an encrypted volume, with guest-only decryption keys" fr "sur un disque chiffré, où seul la machine virtuelle a les clefs"
            at_step PI_API_CHANGE
            bold "Architectural changes " fr "Changement d'architecture "
            $ { text "to delegate PullImage to the runtime, implemented in "  fr "pour déléguer PullImage au runtime, codé pour "; code "containerd"; text ", but patches rejected" fr "mais le code a été rejetée" }

        at_step PI_COMPLETE
        translate 150, -350, 0
        stamp "orange", "Rework!" fr "À revoir"


// ----------------------------------------------------------------------------
base_slide "Attestation" fr "Attestation",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Measuring what we run using cryptography" fr "Mesurer pour contrôler ce qui tourne"
    slide_picture "Measuring.png", 3%, 300
    stepping

    kata_containers integer(KC_ATTEST + smooth_step + 0.5)
    contents 0,
        at_step 1
        talk_frame
            at_step AT_PREATTEST
            color "#C22"
            bold "Preattestation: " fr "Pré-attestation: "
            color "black"
            $ "Measure the payload before allowing it to start (original SEV)" fr "Mesure le contenu avant d'autoriser l'exécution (SEV)"

            at_step AT_POSTATTEST
            color "#C62"
            bold "Postattestation: " fr "Post-attestation: "
            color "black"
            $ "Code in the payload can confirm its identity using the measurement in order to get secrets" fr "Le code peut s'identifier à l'aide d'une mesure pour obtenir des secrets"

            at_step AT_WORKLOAD
            color "#2A2"
            bold "Workload attestation" fr "Attestation de contenu"
            color "black"
            $ "The workload itself is attested, e.g. gets secrets from attestation" fr "L'application ne se lance qu'après attestation (par ex. parce qu'elle a reçoit des secrets de l'attestation)"


// ----------------------------------------------------------------------------
base_slide "How does attestation work?" fr "Comment marche l'attestation",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Challenge / response to deliver secrets" fr "Question / Réponse pour donner les secrets"
    slide_picture "Customs.jpg", 3%, 300
    stepping

    kata_containers integer(KC_MSMTS + smooth_step + 0.5)
    contents 0,
        at_step 1
        talk_frame
            at_step MS_MEASURING
            bold "Cryptographic measurement: " fr "Mesure cryptographique "
            $ "Measurement of relevant memory performed by hardware / firmware" fr "Memory is measured by hardware / firmware"
            at_step MS_CHALLENGE
            bold "Cryptographic challenge: " fr "Vérification cryptogrpahique "
            $ "Send proof of identity (salted)" fr "Envoi d'une preuve d'identité (avec nonce)"

            at_step MS_KEY_DELIVERY
            bold "Secret delivery " fr "Révéler des secrets "
            $ "Ensure the workload cannot do harm if not attested" fr "Guarantir que le contenu est inoffensif avant attestation"

            at_step MS_REMOTE
            bold "Remote attestation: " fr "Attestation distante "
            $ "Can invalidate workloads e.g. if compromized" fr "Peut invalider un contenu vulnérable"





// ============================================================================
//
section_slide "Conclusion" fr "Conclusion",
//
// ============================================================================
    title page_label
    subtitle
        $ "Confidential Containers are an opportunity" fr "Les conteneurs sécurisés, une opportunité"
        $ "Let's see how this works in practice" fr "Il est temps de mettre ça en pratique"


// ---------------------------------------------------------------------------
base_slide "Key takeaways" fr "Ce qu'il faut retenir",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "High cost of confidentiality and sandboxing" fr "Un coût élevé pour la sécurité et l'isolation"
    slide_picture "Pile of Cash.png", 0%, 250
    stepping
    story
        * "Relase 1 of confidential containers is around the corner"
        * "Practical, portable, uniform attestation is a challenge"
        * "Performance issues are mostly vastly enhanced resource usage"
        * "Image download is a huge contributor to this"
        * "Access control has to be re-thought (not in release 1)"
        * "Debuggability or confidentiality, pick one"



// ============================================================================
//
section_slide "Thank you" fr "Merci",
//
// ============================================================================
    title page_label
    subtitle "Let's get started with the workshop"
    stepping

    contents 0,
        text_box 200, -400, 600, 200,
            style "story"
            font_size 30
            anchor
                color "white"
                image -80, -40, 10%, 10%, "Tao3D.png"
            paragraph "This Tao3D presentation is available at" fr "Cette présentation peut être téléchargée à "
            paragraph presentation_tao

        color "white"
        image 700, -230, 120%, 120%, "QR Cocde Coco Workshop.png"

    translate_y -50
    title_slide_logo


// ============================================================================
//
section_slide "Overflow sides" fr "En savoir plus",
//
// ============================================================================
    title page_label
    subtitle presentation_url
    stepping

    contents 0,
        text_box 200, -400, 600, 200,
            style "story"
            font_size 30
            anchor
                color "white"
                image -80, -40, 10%, 10%, "Tao3D.png"
            paragraph "This Tao3D presentation is available at" fr "Cette présentation peut être téléchargée à "
            paragraph presentation_tao

    translate_y -50
    title_slide_logo


// ----------------------------------------------------------------------------
base_slide "So, what is the problem?" fr "Du coup, c'est quoi le problème?",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "It looks like you got everything under control" fr "On dirait que tout est sous contrôle"
    stepping
    picture
        image 0, 30, 140%, 140%, "So what is the problem.jpg"


// ============================================================================
//
section_slide "Let's start with attestation" fr "Commençons par l'attestation",
//
// ============================================================================
    title page_label
    subtitle "The problem with standards..."
    stepping
    page_quote 1


// ----------------------------------------------------------------------------
base_slide "Attestation is a very general thing" fr "L'attestation est un principe général",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "So there is More One Way To Do It™"
    slide_picture "Attestation.png", 3%, 300

    stepping
    story
        * "Remote Attestation Procedures (RATS)"
        ** "A relatively complex topic in itself"
        * "Secure Boot and Trusted Platform Module"
        ** "How about manufacturing transient virtual TPMs?"
        $
        * "Confidential Containers have their own attestation"
        ** "Which needs to plug into all existing technologies"
        $
        color "darkgreen"
        * "Hide platform differences from user-space"

    right_picture
        images "RATS.jpg", "RATS roles.png", "TPM.png", "Magic Smoke.jpg", "CCont Attestation.png", "CCont Attestation platforms.jpg", "Hard Drive Abstraction.png"


// ----------------------------------------------------------------------------
base_slide "Attestation and Key Brokering",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Attestation is robust only if it hands you secrets"
    slide_picture "Keys.png", 3%, 300

    stepping
    story
        * "Attestation Service (AS, aka Verifier): Checks your ID"
        * "Key Broker Service (KBS): Hands over secrets"
        * { text "Your workload must "; italic "need"; text " that secret" }
        $
        * "KBS protocol defined by Confidential Containers"
        ** "... first implemented by Sergio Lopez for libkrun"
        $
        * "Services will come from different players"
        color "darkgreen"
        ** "So we need to define protocols, not just code"


    right_picture
        images "Attestation Service.png", "Key Broker Service.png", "Gossip.jpg", "KBS Protocol.png", "Attestation Server.png", "Azure Attestation.png", "Protocols.png"


// ============================================================================
//
section_slide "Then there is performance" fr "Ensuite, il y a la performance",
//
// ============================================================================
    title page_label
    subtitle "A hardware vendor's dream..."
    stepping
    page_quote 2


// ----------------------------------------------------------------------------
base_slide "The cost of confidentiality" fr "Combien ça coûte de sécuriser?",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Confidentiality gets in the way of deduplication" fr "Le chiffrage empêche la déduplication"
    slide_picture "Good Cheap Fast.png", 3%, 300
    stepping

    story
        * "Downloaded images are now encrypted" fr "Les images de conteneur sont chiffrées"
        ** "So they can't be shared between pods" fr "Donc elles ne peuvent pas être réutilisées"
        ** "This increases disk and networking costs" fr "Cela augment les coûts de disque et de réseau"
        * "Disks too must be encrypted" fr "Les disques sont aussi cryptés"
        ** "Therefore, no deduplication" fr "Donc pas de déduplication"
        * "Memory is encrypted too" fr "La mémoire aussi est chiffrée"
        ** "No kernel same-page merging" fr "Pas de KSM (unification des pages identiques)"
        $
        color "darkgreen"
        * "Who has a patch for Crypto KSM?"


    right_picture
        images "Canal+ Scrambled.jpg", "Disk image sharing.png", "Nydus Image download.png", "Disk Encryption.jpg", "Disk Deduplication.png", "Memory Encryption.jpg", "KSM Efficiency.png", "Crypto KSM.jpg"


RUNTIME_SCALE -> 3 + 5 * fade_at(smooth_step, 1)

// ----------------------------------------------------------------------------
base_slide "Good news: manageable runtime cost",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "We encrypt memory on the fly, but it's ""cheap"""
    slide_picture "Pile of Cash.png", 3%, 300
    stepping

    picture
        text_box -300, 300, 900, 200,
            style "story"
            color "#444"
            $ { rounded_rectangle 25, 8, 50, 20,  0; text "   Regular virtual machine" }
            $ { rounded_rectangle 25, 8, 50, 20, 6;  text "   Confidential Workload (krun)" }

    picture
        line_color "lightblue", 80%
        translate 0, 100, 0
        style "story"
        font_size 24
        for Y in -20..20 loop locally
            show fade_out_at(RUNTIME_SCALE * abs Y, 33) * fade_out_at((abs Y mod 2), 1 + smooth_step)
            line -850, RUNTIME_SCALE * 10 * Y, 950, RUNTIME_SCALE * 10 * Y
            text_box -980, RUNTIME_SCALE * 10 * Y, 300, 100,
                align 100%
                vertical_align 50%
                color "darkblue"
                text "" & (Y * 5) & "%"

    picture
        sev_step -> 0
        sev_step := 0

        load_csv "SEV Runs.csv", "sev_runs"
        sev_runs Name:text,Container:real,Workload:real,VM:real,Name2:text,PctCC:real,PctVM:real ->
            translate_x 70
            sev_step := sev_step + 1
            refresh 0.0
            locally
                show fade_at(page_time * 15, sev_step)
                translate -850, -200, 0
                rotate_z 65
                text_box 0, 0, 120, 80,
                    style "story"
                    font_size 24
                    align 100%
                    text Name

            locally
                translate -850, 100, 0
                show fade_at(page_time * 14, sev_step)
                color_hsv 60 - 1.5 * PctVM, 70%, 60%
                rounded_rectangle -12, PctVM * RUNTIME_SCALE, 20, PctVM * RUNTIME_SCALE * 2, 0
                color_hsv 60 - 1.5 * PctCC, 70%, 60%
                rounded_rectangle  12, PctCC * RUNTIME_SCALE, 20, PctCC * RUNTIME_SCALE * 2, 6
        sev_runs X -> false
        at_step 1



// ============================================================================
//
section_slide "Image (re)download",
//
// ============================================================================
    title page_label
    subtitle "The ""Lather, Rinse, Repeat"" school of programming"
    stepping
    page_quote 3


// ----------------------------------------------------------------------------
base_slide "Cache + Dedup + Crypto?",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Can we get something like Nydus on encrypted blocks?"
    slide_picture "Crazy Frog.jpg", 3%, 300
    stepping

    picture
        scale fade_in(page_time, 2)
        translate_x -3000 * smooth_step
        image 0, 60, 85%, 85%, "Nydus image download.png"
        at_step 1
        image 3000, 60, 95%, 95%, "Nydus design.png"
        image 6000, 60, 125%, 125%, "Device Mapper.jpg"
        image 9000, 60, 85%, 85%, "Attest and Decrypt Whole Layer.jpg"
        image 12000, 20, 65%, 65%, "Building Signed Images.jpg"
        image 15000, 20, 50%, 50%, "Block-based Image Download.jpg"

        at_step 5


// ============================================================================
//
section_slide "Access Control",
//
// ============================================================================
    title page_label
    subtitle "We just need to rewrite the Kubernetes access-control model from scratch"
    stepping
    page_quote 4


// ----------------------------------------------------------------------------
base_slide "Going from two to three",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "It's the beginning of counting, folks"
    slide_picture "Counting.jpg", 3%, 300
    stepping

    on "pageentry",
        skip_to CC_PLATFORM

    kata_containers integer(KC_CONFIDENTIAL + smooth_step + 0.5)
    contents 0,
        at_step CC_PLATFORM
        talk_frame
            color "#C22"
            rectangle 0, 7, 50, 20
            bold " Trusted platform: " fr " Plateforme sécurisée: "
            color "black"
            $ "Offers confidentiality guarantees enforced by hardware cryptography" fr "Offre des garanties de sécurité à l'aide de cryptographie matérielle"
            at_step CC_HOST
            color "#24C"
            rectangle 0, 7, 50, 20
            bold " Host: " fr " Hébergeur: "
            color "black"
            $ "Manages and offers resources used to run containers (CPU, memory, I/O, etc)" fr "Gère et présente les resources pour les conteneurs (CPU, mémoire, E/S, etc)"
            at_step CC_TENANT
            color "#2A2"
            rectangle 0, 7, 50, 20
            bold " Tenant: " fr " Locataire: "
            color "black"
            $ "Confidential area, inaccessible to the host even when running on it" fr "Environnement confidentiel, inaccessible à l'hébergeur même lorsqu'elle l'utilise"

// ----------------------------------------------------------------------------
base_slide "The host/tenant split API",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Let's just decide which credentials we use on the fly"
    slide_picture "Crazy Frog.jpg", 3%, 300
    stepping

    picture
        scale fade_in(page_time, 2)
        translate_x -3000 * smooth_step
        image 0, 30, 55%, 55%, "Host-tenant Split API.jpg"
        at_step 1


// ============================================================================
//
section_slide "Debuggability",
//
// ============================================================================
    title page_label
    subtitle "The FBI school of debugging"
    stepping
    page_quote 5


// ----------------------------------------------------------------------------
base_slide "Tell us what you need to know",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "and we'll teach you why you don't need it"
    slide_picture "Don't Panic.jpg", 3%, 300
    stepping

    picture
        scale fade_in(page_time, 2)
        translate_x -3000 * smooth_step
        image 0, 30, 135%, 135%, "Debugging Nightmare.png"
        at_step 1
        image 2500, 30, 75%, 75%, "Don't Panic Is A Command.png"
        text_box 3400, 0, 800, 600,
            style "story"
            $ "It's not a suggestion, it's a rule"
            $ "Really, we mean it"
            $
            $ { text "if you " ; code "panic()" ; text ", we offer a cryptographic, hardware-enforced guarantee that you that you won't see the logs" }
            $
            color "darkgreen"
            $ "Any good ideas on how to address this in a sensible manner is welcome"


// ----------------------------------------------------------------------------
base_slide "Attestation process" fr "Le processus d'attestation",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Exchanging keys with the relying party" fr "Comment échanger ses clefs en toute sécurité"
    slide_picture "Key Exchange.jpg", 3%, 300
    stepping

    one_picture
        image 0, 0, 75%, 75%, "Kata Attestation.png"

// ----------------------------------------------------------------------------
base_slide "Immutable Pods",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "You cannot change what you certified"
    slide_picture "Certified.png", 3%, 300
    stepping

    one_picture
        image 0, 0, 70%, 70%, "Kata Immutable Pods.png"

// ----------------------------------------------------------------------------
base_slide "Two Control Planes",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Host and Tenant security realms"
    slide_picture "Two Steering Wheels.jpg", 3%, 300
    stepping

    one_picture
        image 0, 0, 70%, 70%, "Kata Dual Control Plane.png"


// ============================================================================
//
section_slide "Performance considerations",
//
// ============================================================================
    title page_label
    subtitle "Impact of running virtualized"


// ============================================================================
//
section_slide "Memory density",
//
// ============================================================================
    title page_label
    subtitle "How many containers can you fit?"


memory_axis ->
    if stacked_item_sum > 5120 then
        reference_scale 6144, "6G"
        reference_tick  5120, "5G"
        reference_tick  4096, "4G"
        reference_tick  3072, "3G"
        reference_tick  2048, "2G"
        reference_tick  1024, "1G"
    else if stacked_item_sum > 4096 then
        reference_scale 5120, "5G"
        reference_tick  4096, "4G"
        reference_tick  3072, "3G"
        reference_tick  2048, "2G"
        reference_tick  1024, "1G"
    else if stacked_item_sum > 3072 then
        reference_scale 4096, "4G"
        reference_tick  3072, "3G"
        reference_tick  2048, "2G"
        reference_tick  1024, "1G"
    else if stacked_item_sum > 2048 then
        reference_scale 3072, "3G"
        reference_tick  2048, "2G"
        reference_tick  1024, "1G"
    else if stacked_item_sum > 1024 then
        reference_scale 2048, "2G"
        reference_tick  1024, "1G"
    else
        reference_scale 1024, "1G"
        reference_tick   512, "512M"


MEM_START       -> 0
MEM_HOST        -> 1
MEM_CONMON      -> 2
MEM_BASH        -> 3
MEM_NGINX       -> 4
MEM_KUBELET     -> 5
MEM_CRIO        -> 6
MEM_x100        -> 7
MEM_x1          -> 8
MEM_RUNTIME     -> 9
MEM_SHIM        -> 10
MEM_QEMU        -> 11
MEM_VIRTIOFSD   -> 12
MEM_GUEST       -> 13
MEM_AGENT       -> 14
MEM_RUNTIME_OUT -> 15
MEM_DNF_INSTALL -> 16
MEM_SCALEx100   -> 17
MEM_LOADx100    -> 18
MEM_CONMONx100  -> 19
MEM_SHIMx100    -> 20
MEM_SHIMv2      -> 21
MEM_LAST        -> 22

mem_host        -> anim 0.0, MEM_START=0, MEM_HOST=950
mem_kubelet     -> anim 0.0, MEM_START=0, MEM_KUBELET=110
mem_crio        -> anim 0.0, MEM_START=0, MEM_CRIO=28
mem_conmon      -> anim 0.0, MEM_START=0, MEM_CONMON=2.5, MEM_x100=250, MEM_x1=2.5, MEM_CONMONx100=250, MEM_SHIMv2=0.0
mem_shim        -> anim 0.0, MEM_START=0, MEM_SHIM=16.8, MEM_SHIMx100=1680, MEM_SHIMv2=19.2
mem_workload    -> anim 0.0, MEM_START=0, MEM_BASH=4.7, MEM_NGINX=8.4, MEM_x100=840, MEM_x1=8.4, MEM_LOADx100=840
mem_runtime     -> anim 0.0, MEM_START=0, MEM_RUNTIME=25, MEM_RUNTIME_OUT=0
mem_qemu        -> anim 0.0, MEM_START=0, MEM_QEMU=30
mem_virtiofsd   -> anim 0.0, MEM_START=0, MEM_VIRTIOFSD=10, MEM_DNF_INSTALL=350
mem_guest       -> anim 0.0, MEM_START=0, MEM_GUEST=300, MEM_DNF_INSTALL=880
mem_agent       -> anim 0.0, MEM_START=0, MEM_AGENT=60
mem_scale       -> anim 0.1, MEM_START=0.7, MEM_KUBELET=0.35, MEM_x100=0.3, MEM_SCALEx100=0.15

name_workload   -> anim_text "Bash", MEM_NGINX="nginx", MEM_x100="nginx x 100", MEM_x1="nginx", MEM_LOADx100="nginx x 100"
name_conmon     -> anim_text "Conmon", MEM_x100="Conmon x 100", MEM_x1="Conmon", MEM_CONMONx100="Conmon x 100"
name_shim       -> anim_text "Kata Shim", MEM_SHIMx100="Kata Shim x100", MEM_SHIMv2="shim-v2"

mem_subtitle    -> anim_text "Death by a thousand cuts", MEM_BASH="Running bash", MEM_NGINX="Running a web server", MEM_KUBELET="Kubernetes overhead", MEM_x100="Running 100 containers", MEM_x1="Overhead varies with number of containers", MEM_RUNTIME="Kata adds even more overhead", MEM_RUNTIME_OUT="Kata Runtime is transient", MEM_DNF_INSTALL="I/Os consume memory (buffers, etc)", MEM_SCALEx100="Running 100 containers", MEM_SHIMv2="Kata Containers 2.0: Containerd Shim v2"

mem_cmdline     -> anim_text "", MEM_CONMON="podman run -it --rm fedora bash", MEM_NGINX="podman run -dt --pod new:nginx -p8080:80 nginx", MEM_KUBELET="kubectl apply -f nginx.yaml", MEM_x100="(yaml file with 100x nginx)", MEM_x1="kubectl apply -f nginx.yaml", MEM_RUNTIME="podman run -it --rm --runtime kata fedora bash", MEM_DNF_INSTALL="fedc07a13# dnf install -y procps-ng", MEM_SCALEx100="(runtimeClass kata, 100x nginx)", MEM_SHIMv2="containerd-shim-kata-v2"

// ---------------------------------------------------------------------------
base_slide "Memory usage",
// ----------------------------------------------------------------------------
    title page_label
    subtitle mem_subtitle
    slide_picture "Memory.jpg", 10%, 250
    stepping
    picture
        anim_step integer(smooth_step + 0.5)
        stacked_items "MB", mem_scale,
            stacked mem_host, "Host", "Host kernel and user-space programs"
            stacked mem_kubelet, "Kubelet", "Top-level control process"
            stacked mem_crio, "CRI-O", "Container runtime interface"
            stacked mem_runtime, "Kata Runtime", "Kata runtime (transient)"
            stacked mem_conmon, name_conmon, "Process monitoring your container"
            stacked mem_virtiofsd, "virtiofsd", "Host file access from guest"
            stacked mem_shim, name_shim, "I/O interface for container"
            stacked mem_qemu, "QEMU", "Virtual machine monitor"
            stacked mem_guest, "Guest", "Guest OS kernel, data, user-space"
            stacked mem_agent, "Agent", "Kata agent in the guest"
            stacked mem_workload, name_workload, "Actual workload you are running"
        translate 0, -300, 0
        memory_axis
        at_step MEM_LAST

    picture
        text_box 0, 380, 1200, 100,
            style "story"
            align 0.5
            code mem_cmdline

    picture
        only_at MEM_DNF_INSTALL..MEM_LOADx100,
            text_box 750, 0, 400, 500,
                style "story"
                color tao "red"
                text "Possible double accounting of the same physical memory between guest and virtiofsd"


memory_crio      -> 53         // 28 + 2.5x10
memory_k8s       -> 163        // memory_crio + 110
memory_1pod_k1   -> 561        // memory_crio + 16.8x10 + (30 + 10 + 300)
memory_1pod_k1L  -> 911        // memory_1pod_k1L + 350
memory_10pods_k1 -> 921        // 1pod + 9*(30 + 10)
memory_10pods_k1L-> 3961       // 10pods + 9*350
memory_1pod_k2   -> 112        // memory_crio + 19.2 + 30 + 10
memory_10pods_k2 -> 645        // memory_crio + 10 * (19.2 + 30 + 10)

// ---------------------------------------------------------------------------
base_slide "Memory - Comparing runtimes",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Cost of the runtime to run 10 instances of nginx"
    slide_picture "Memory.jpg", 10%, 250
    stepping
    picture
       stacked_item_scale := 0.5 - 0.35 * fade_at(smooth_step, 7)
       stacked_item_sum := 0.0

       translate 0, -200, 0

       bar_graph_unit "M"

       bar_graph 0, memory_crio,        "CRI-O"
       bar_graph 1, memory_k8s,         "Kubernetes"
       bar_graph 2, memory_1pod_k1,     "One pod, Kata 1.12"
       bar_graph 3, memory_10pods_k1,   "Ten pods, Kata 1.12"
       bar_graph 4, memory_1pod_k2,     "One pod, Kata 2.0"
       bar_graph 5, memory_10pods_k2,   "Ten pods, Kata 2.0"

       last_graph 6,
           * "Kata requires a lot of memory for qemu, guest kernel and virtiofs."
           * "Reusing pods can help, but is not common practice."

       memory_axis


// ---------------------------------------------------------------------------
base_slide "Memory - disk caches",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Perform a dnf install inside the running pods"
    slide_picture "Memory.jpg", 10%, 250
    stepping
    picture
       stacked_item_scale := 0.5 - 0.35 * fade_at(smooth_step, 3)
       stacked_item_sum := 0.0

       translate 0, -200, 0

       bar_graph_unit "M"

       bar_graph 0, memory_1pod_k1,     "One pod, Kata 1.12"
       bar_graph 1, memory_10pods_k1,   "Ten pods, Kata 1.12"
       bar_graph 2, memory_1pod_k1L,    "One pod, after I/Os"
       bar_graph 3, memory_10pods_k1L,  "Ten pods, after I/Os"

       last_graph 4,
           * "I/O buffers consume a lot of memory."
           * "DAX allows host and guests to share that memory"

       memory_axis


// ---------------------------------------------------------------------------
base_slide "Memory overhead - It's bad",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Huge effort needed to make things better"
    slide_picture "Memory.jpg", 10%, 250
    stepping
    story
        * "Too many processes"
        ** "Partially addressed by 2.0"
        ** "... but single point of failure"
        * "A lot of memory in buffer caches"
        ** "DAX allows host buffer caches to be used"

    code_move 300, 0, 1200, 700
    code_box_at_step 1, "Processes", { text load_text "conmon-dump.log" }
    code_box_at_step 2..3, "Kata Shim v2", { text load_text "conmon-dump-v2.log" }


// ============================================================================
//
section_slide "Boot time",
//
// ============================================================================
    title page_label
    subtitle "How fast does a container start?"


boot_axis ->
    if stacked_item_sum > 20 then
        reference_scale 30, "30s"
        reference_tick  20, "20s"
        reference_tick  10, "10s"
    else if stacked_item_sum > 15 then
        reference_scale 20, "20s"
        reference_tick  15, "15s"
        reference_tick  10, "10s"
        reference_tick  5, "5s"
    else if stacked_item_sum > 10 then
        reference_scale 15, "15s"
        reference_tick  10, "10s"
        reference_tick  5, "5s"
    else if stacked_item_sum > 5 then
        reference_scale 10, "10s"
        reference_tick  8, "8s"
        reference_tick  6, "6s"
        reference_tick  4, "4s"
        reference_tick  2, "2s"
    else if stacked_item_sum > 2 then
        reference_scale 5, "5s"
        reference_tick  4, "4s"
        reference_tick  3, "3s"
        reference_tick  2, "2s"
        reference_tick  1, "1s"
    else if stacked_item_sum > 1 then
        reference_scale 2, "2s"
        reference_tick  1, "1s"
    else
        reference_scale 1,   "1s"
        reference_tick  0.5, "0.5"


// ---------------------------------------------------------------------------
base_slide "Boot time overhead",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Time to boot containers"
    slide_picture "Boot.jpg", 10%, 250

    stepping
    picture
        anim_step integer(smooth_step + 0.5)
        boot_scale -> anim 0.0, 0=600.0, 1=50.0, 3=30
        stacked_item_scale := boot_scale
        stacked_item_sum := 0.0

        translate 0, -200, 0

        bar_graph_unit "s"

        bar_graph 0, boot_1busybox_runc,         "1x busybox on runc"
        bar_graph 1, boot_1busybox_kata,         "1x busybox on Kata"
        bar_graph 2, boot_20busybox_runc,        "20x busybox on runc"
        bar_graph 3, boot_20busybox_kata,        "20x busybox on Kata"

        last_graph 4,
            * "Pod boot time is relatively constant (about 7s here)"
            * "But can you run multiple containers in a single pod?"

        boot_axis

// ---------------------------------------------------------------------------
base_slide "Run time overhead",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Shutting down also takes time"
    slide_picture "Boot.jpg", 10%, 250

    stepping
    picture
        anim_step integer(smooth_step + 0.5)
        run_scale -> anim 0.0, 0=50.0, 1=30, 2=15
        stacked_item_scale := run_scale
        stacked_item_sum := 0.0

        translate 0, -200, 0

        bar_graph_unit "s"

        bar_graph 0, boot_20busybox_runc,        "20x busybox on runc"
        bar_graph 1, boot_20busybox_kata,        "20x busybox on Kata"
        bar_graph 2, run_20busybox_kata,         "Run 20x busybox"
        bar_graph 3, run_20alpine_kata,          "Run 20x alpine"
        bar_graph 4, run_20fedora_kata,          "Run 20x fedora"

        last_graph 5,
            * "Shutting down a Kata pod also takes time"
            * "With Kata, the container base you use is not as relevant"

        boot_axis


//    "http://events17.linuxfoundation.org/sites/events/files/slides/Light%20weight%20virtualization%20with%20QEMU%26KVM_0.pdf"

// ( for ((I=0; I < 100; I++)) do; podman run -it --rm alpine sh -c ; done; )  21.91s user 18.48s system 9% cpu 7:13.45 total

// ( for ((I=0; I < 100; I++)) do; podman run -it --rm --pull=never busybox sh -)  20.31s user 16.98s system 11% cpu 5:19.91 total

// ( for ((I=0; I < 100; I++)) do; podman run -it --rm --pull=never --runtime   )  32.70s user 22.99s system 10% cpu 8:51.73 total


// 20 runc alpine containers on turbo
// real	0m28.406s
// user	0m3.917s
// sys	0m3.346s
run_20alpine_kata -> 28.406

// 20 runc busybox
// real	0m27.390s
// user	0m3.881s
// sys	0m3.262s
run_20busybox_kata -> 27.390

// real	0m29.531s
// user	0m3.910s
// sys	0m3.331s
run_20fedora_kata -> 29.531

// Start 1 container
// real	0m0.361s
// user	0m0.011s
// sys	0m0.028s
boot_1busybox_runc -> 0.361

// real	0m7.175s
// user	0m0.018s
// sys	0m0.045s
boot_1busybox_kata -> 7.175

// Start 20 containers
// real	0m7.921s
// user	0m0.265s
//sys	0m0.525s
boot_20busybox_runc -> 7.921

// real	0m14.349s
// user	0m0.275s
// sys	0m0.539s
boot_20busybox_kata -> 14.349



// ============================================================================
//
section_slide "Disk I/O",
//
// ============================================================================
    title page_label
    subtitle
        $ "Disk performance with virtiofs:"
        $ "improved, but still not good"


disk_axis ->
    if stacked_item_sum > 120 then
        reference_scale 180, "3m"
        reference_tick  120, "2m"
        reference_tick   60, "1m"
        reference_tick   30, "30s"
        reference_tick   10, "10s"
    else if stacked_item_sum > 60 then
        reference_scale 120, "2m"
        reference_tick   90, "1m30"
        reference_tick   60, "1m"
        reference_tick   30, "30s"
    else if stacked_item_sum > 30 then
        reference_scale 60, "1m"
        reference_tick  30, "30s"
    else if stacked_item_sum > 20 then
        reference_scale 30, "30s"
        reference_tick  20, "20s"
        reference_tick  10, "10s"
    else
        reference_scale 20, "20s"
        reference_tick  10, "10s"




// ---------------------------------------------------------------------------
base_slide "Disk read overhead",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Copy files from the local file system"
    slide_picture "Disk.jpg", 10%, 250

    stepping
    picture
        anim_step integer(smooth_step + 0.5)
        disk_tar_scale -> anim 0.0, 0=10.0, 2=5.0, 3=3.0
        stacked_item_scale := disk_tar_scale
        stacked_item_sum := 0.0

        translate 0, -200, 0

        bar_graph_unit "s"

        bar_graph 0,   disk_tar_runc_sys, disk_tar_runc, "runc"
        bar_graph 1,   disk_tar_runc_blk_sys, disk_tar_runc_blk, "runc+blk"
        bar_graph 2,   disk_tar_kata_sys, disk_tar_kata, "Kata+virtiofs"
        bar_graph 3,   disk_tar_kata9p_sys, disk_tar_kata9p, "Kata+9pfs"
        bar_graph 4,   disk_tar_kata_blk_sys, disk_tar_kata_blk, "Kata+blk"

        last_graph 5,
            * "File reads are markedly slower with Kata"
            * "virtiofs improves a lot over 9pfs"
            * "Kata benefits less from blk than runc"

        disk_axis

        // tar read: time (for ((I=0; I < 20; I++)); do echo $I; tar cf /tmp/usr.tar usr; done)
        // runc
        // real	0m34.775s user	0m0.750s sys	0m5.048s
        disk_tar_runc         -> 34.775
        disk_tar_runc_sys     -> 5.048

        // real	1m15.780s user	0m1.136s sys	0m9.727s
        disk_tar_kata         -> 75.780
        disk_tar_kata_sys     -> 9.727

        // real	2m52.740s user	0m1.310s sys	0m10.733s (many errors)
        disk_tar_kata9p       -> 172.740
        disk_tar_kata9p_sys   -> 10.733

        // real	0m20.348s user	0m0.916s sys	0m4.946s
        disk_tar_runc_blk     -> 20.348
        disk_tar_runc_blk_sys -> 4.946

        // real	1m8.298s user	0m1.123s sys	0m9.978s
        disk_tar_kata_blk     -> 68.298
        disk_tar_kata_blk_sys -> 9.978


// ---------------------------------------------------------------------------
base_slide "Disk write overhead",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Write a 1.0G file from /dev/random"
    slide_picture "Disk.jpg", 10%, 250

    stepping
    picture
        anim_step integer(smooth_step + 0.5)
        disk_dd_scale -> anim 0.0, 0=30.0
        stacked_item_scale := disk_dd_scale
        stacked_item_sum := 0.0

        translate 0, -200, 0

        bar_graph_unit "s"

        bar_graph 0,   disk_dd_runc_sys, disk_dd_runc, "runc"
        bar_graph 1,   disk_dd_kata_sys, disk_dd_kata, "Kata+virtiofs"
        bar_graph 2,   disk_dd_kata9p_sys, disk_dd_kata9p, "Kata+9pfs"
        bar_graph 3,   disk_dd_runc_blk_sys, disk_dd_runc_blk, "runc+blk"
        bar_graph 4,   disk_dd_runc_blk2_sys, disk_dd_runc_blk2, "rewrite runc+blk"
        bar_graph 5,   disk_dd_kata_blk_sys, disk_dd_kata_blk, "Kata+blk"
        bar_graph 6,   disk_dd_kata_blk2_sys, disk_dd_kata_blk2, "rewrite Kata+blk"

        last_graph 7,
            * "Kata with virtiofs outperforms runc here"
            * "Overwriting a file is more expensive"
            * "The effect is less marked with Kata"

        disk_axis

        // dd write: dd if=/dev/random of=/usr/glob bs=16384 count=65536
        // runc
        // real	0m15.167s user	0m0.043s sys	0m5.152s
        disk_dd_runc            -> 15.167
        disk_dd_runc_sys        -> 5.152

        // real	0m10.297s user	0m0.077s sys	0m5.902s
        disk_dd_kata            -> 10.297
        disk_dd_kata_sys        -> 5.902

        // real	real	0m18.321s user	0m0.044s sys	0m5.279s
        disk_dd_kata9p          -> 18.321
        disk_dd_kata9p_sys      -> 5.279

        // real	0m5.277s user	0m0.036s sys	0m4.698s
        disk_dd_runc_blk        -> 5.277
        disk_dd_runc_blk_sys    -> 4.698

        // First run: real	0m8.923s user	0m0.051s sys	0m5.073s
        disk_dd_kata_blk        -> 8.923
        disk_dd_kata_blk_sys    -> 5.073

        // Second run (file exists): real	0m15.663s user	0m0.040s sys	0m4.714s
        disk_dd_runc_blk2        -> 15.663
        disk_dd_runc_blk2_sys    -> 4.714

        // Second run real	real	0m9.174s user	0m0.074s sys	0m5.146s
        disk_dd_kata_blk2        -> 9.174
        disk_dd_kata_blk2_sys    -> 5.146



// ============================================================================
//
section_slide "Networking I/O",
//
// ============================================================================
    title page_label
    subtitle
        $ "Multiple networking configurations"
        $ "At least some of them should perform well"


net_axis ->
    if stacked_item_sum > 240 then
        reference_scale 360, "6m"
        reference_tick  180, "3m"
        reference_tick   60, "1m"
    else if stacked_item_sum > 120 then
        reference_scale 180, "3m"
        reference_tick  120, "2m"
        reference_tick   60, "1m"
        reference_tick   30, "30s"
        reference_tick   10, "10s"
    else if stacked_item_sum > 60 then
        reference_scale 120, "2m"
        reference_tick   90, "1m30"
        reference_tick   60, "1m"
        reference_tick   30, "30s"
    else if stacked_item_sum > 30 then
        reference_scale 60, "1m"
        reference_tick  30, "30s"
    else if stacked_item_sum > 20 then
        reference_scale 30, "30s"
        reference_tick  20, "20s"
        reference_tick  10, "10s"
    else
        reference_scale 20, "20s"
        reference_tick  10, "10s"


// ---------------------------------------------------------------------------
base_slide "Network overhead",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Read or write a 1G file from local server"
    slide_picture "Network card.png", 10%, 250

    stepping
    picture
        anim_step integer(smooth_step + 0.5)
        net_read_scale -> anim 0.0, 0=10.0, 6=1.5
        stacked_item_scale := net_read_scale
        stacked_item_sum := 0.0

        translate 0, -200, 0

        bar_graph_unit "s"

        bar_graph 0,   net_read_runc_sys, net_read_runc, "runc read"
        bar_graph 1,   net_read_kata_sys, net_read_kata, "kata read"
        bar_graph 2,   net_write_runc_sys, net_write_runc, "runc write"
        bar_graph 3,   net_write_kata_sys, net_write_kata, "kata write"
        bar_graph 4,   net_local_read_runc_sys, net_local_read_runc, "runc read host"
        bar_graph 5,   net_local_read_kata_sys, net_local_read_kata, "kata read host"
        bar_graph 6,   net_local_write_runc_sys, net_local_write_runc, "runc write host"
        bar_graph 7,   net_local_write_kata_sys, net_local_write_kata, "kata write host"

        last_graph 8,
            * "Kata overhead is modest"
            ** "Lower system usage in guest (moved to host)"
            * "Host networking is rather bad for Kata"
            ** "Writing is really bad"

        net_axis


        // runc read real	0m42.238s user	0m2.833s sys	0m8.870s
        net_read_runc            -> 42.328
        net_read_runc_sys        -> 8.870

        // kata read real	0m43.324s user	0m2.590s sys	0m6.145s
        net_read_kata            -> 43.324
        net_read_kata_sys        -> 6.145

        // runc write: real	0m41.703s user	0m2.455s sys	0m2.952s
        net_write_runc           -> 41.703
        net_write_runc_sys       -> 2.952

        // kata write: real	0m43.462s user	0m2.931s sys	0m3.712s
        net_write_kata           -> 43.462
        net_write_kata_sys       -> 3.712

        // Runs from local diskL writes are really bad

        // scp from 10G disk: real	0m38.310s user	0m11.616s sys	0m27.543s
        net_local_read_runc            -> 38.310
        net_local_read_runc_sys        -> 27.543

        // scp from 10G disk: real	0m56.346s user	0m13.306s sys	0m29.037s
        net_local_read_kata            -> 56.346
        net_local_read_kata_sys        -> 29.037

        // scp to 10G disk: real	4m53.355s user	0m8.569s sys	0m19.197s
        net_local_write_runc           -> 293.355
        net_local_write_runc_sys       -> 19.197

        // scp to 10G disk: real	5m30.406s user	0m25.134s sys	0m48.085s
        net_local_write_kata           -> 338.406
        net_local_write_kata_sys       -> 48.085


// ----------------------------------------------------------------------------
base_slide "Network bandwidth",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Path length increase make it harder to saturate link"
    slide_picture "Network card.png", 3%, 300
    stepping

    one_picture
        image 0, 0, 70%, 70%, "Kata Network Bandwidth.png"

        translate 0, -400, 0
        last_graph 2,
            * "Hot-plugged vCPUs don't help much"
            * "Increased path length, notably on the host"

// ----------------------------------------------------------------------------
base_slide "Network latency",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Strong dependency on cgroup limits"
    slide_picture "Network card.png", 3%, 300
    stepping

    one_picture
        image -250 * fade_at(smooth_step, 2), 50, 60%, 60%, "Kata Network Latency.png"

        translate 0, -400, 0
        last_graph 2,
            * "Need > 2 VCPUs"
            * "CPU constraints hurt"


// ---------------------------------------------------------------------------
base_slide "Sandboxing workloads" fr "Toujours plus d'isolation",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "From your software to containers in VMs..." fr "Du logiciel au tombe-du-ciel"
    slide_picture_at OWS_SW..OWS_OS,      "Emoji=Happy.png", 5%, 300
    slide_picture_at OWS_CHROOT..OWS_KVM, "Emoji=Thinking.png", 5%, 300
    slide_picture_at OWS_CNTN..OWS_CRI,   "Emoji=Questioning.png", 4%, 350
    slide_picture_at OWS_KVIRT..OWS_KATA, "Emoji=Crazy.png", 3%, 300
    stepping
    picture
        caption_at OWS_SW, "Software is what matters" fr "Le logiciel, c'est ça qui compte vraiment"
        caption_at OWS_HW, "Direct hardware access (Original Mac, Unikernel)" fr "Accès direct au matériel (MS-DOS, Unikernel)"
        caption_at OWS_CLASH, "Sharing resources is both hard and risky" fr "Partager les resources est difficile et risqué"
        caption_at OWS_OS, "Operating system deal with this problem" fr "C'est le rôle du système d'exploitation"
        caption_at OWS_CHROOT, "chroot gives you a filesystem sandbox" fr "On peut isoler une partie des fichierts"
        caption_at OWS_VIRT, "Virtualization gives you a full machine sandbox" fr "On peut isoler en créant une machine virtuelle"
        caption_at OWS_QEMU, "QEMU: User-space interface for virtualization" fr "QEMU: première solution de virtualisation complète en logiciel libre"
        caption_at OWS_KVM, "KVM: Kernel component supporting virtualization" fr "KVM: support de la virtualisation directement dans le noyau Linux"
        caption_at OWS_CNTN, "Containers are complete OS-built sandboxes" fr "Les conteneurs sont une isolation par le système"
        caption_at OWS_MANY, "Containers scale well" fr "Ce qui est bien, c'est qu'on peut empiler vraiment beaucoup de conteneurs"
        caption_at OWS_DOCKER, "Containers provides rich software components composition and setup" fr "Les composants logiciels: distribution et assemblage"
        caption_at OWS_K8S, "OpenShift features large-scale orchestration" fr "Faire marcher tout ça demande une orchestration"
        caption_at OWS_CRI, "A modular interface connects orchestration to system and workloads" fr "Une architecture applicative repensée pour gérer tout ça"
        caption_at OWS_KVIRT, "You can orchestrate legacy workloads using KubeVirt virtual machines" fr "Pour les vieilles applis, retour à la virtualisation"
        caption_at OWS_KATA, "Kata Containers run sandboxed containers in isolated virtual machines" fr "Isoler les conteneurs par la virtualisation (Kata)"
        overview integer(smooth_step + 0.5)




// ============================================================================
//
//   Overview diagram
//
// ============================================================================
// 0: Your software on hardware
// 1: Your software conflicting with other software
// 2: Operating system: takes control
// 3: Chroot: Carve out your little disk space
// 4: Virtualization: Carve out your whole machine
// 5: qemu
// 6: kvm
// 7: Containers: Carve out the whole thing
// 8: Multiplication of containers
// 9: Docker: declarative software components (dockerd)
// 10: Kubernetes: declarative orchestration (kubelet)
// 11: CRI / CRI-O: Runtime abstraction
// 12: KubeVirt: manage VMs with k8s
// 13: Kata: put containers in their own VM
// 14: qemu-mini

OWS_SW          -> 0
OWS_HW          -> 1
OWS_CLASH       -> 2
OWS_OS          -> 3
OWS_CHROOT      -> 4
OWS_VIRT        -> 5
OWS_QEMU        -> 6
OWS_KVM         -> 7
OWS_CNTN        -> 8
OWS_MANY        -> 9
OWS_DOCKER      -> 10
OWS_K8S         -> 11
OWS_CRI         -> 12
OWS_KVIRT       -> 13
OWS_KATA        -> 14
OWS_MINI        -> 15

overview Step:integer ->
// ----------------------------------------------------------------------------
//   Overview diagram for "simplified overview" slide
// ----------------------------------------------------------------------------
    anim_step Step

    overview_adjust S:real ->
        translate 0, -100 * S, -50 * S
        scale 100% - 30% * S

    locally
        overview_adjust anim(0.0, OWS_SW = 0, OWS_DOCKER = 1)

        locally
            at_step OWS_HW
            soft_box -630, -220, redhat_red2,
                image 0, 0, 60%, 60%, "Hard Disk.png"
            soft_box -210, -220, redhat_red2,
                image 0, 0, 30%, 30%, "CPU.png"
            soft_box 210,  -220, redhat_red2,
                image 0, 0, 30%, 30%, "RAM.png"
            soft_box 630, -220, redhat_red2,
                image 0, 0, 50%, 50%, "Network card.png"

        COUNT -> anim 1.0, OWS_SW = 1, OWS_MANY = 12, OWS_KVIRT = 4, OWS_KATA=8

        ys_x -> anim    0.0, OWS_SW =    0, OWS_CLASH = -630, OWS_VIRT = -630, OWS_CNTN = -630, OWS_KVIRT=-630
        ys_y -> anim    0.0, OWS_SW =    0, OWS_OS    =  220, OWS_VIRT =  250, OWS_CNTN =  220, OWS_KVIRT= 250
        ys_w -> anim 1650.0, OWS_SW = 1650, OWS_CLASH =  400, OWS_VIRT =  400, OWS_CNTN =  400, OWS_KVIRT= 400
        ys_h -> anim  200.0, OWS_SW =  200,                   OWS_VIRT =  150, OWS_CNTN =  200, OWS_KVIRT= 150
        for I in 0..integer(COUNT)-1 loop locally
            translate I * 420 mod 1680 + I / 4 * 30, I / 4 * 50, -I/4 * 200
            show exp(-0.1 * I)
            soft_box ys_x, ys_y, ys_w, ys_h, redhat_blue1, "Your software" fr "Votre logiciel"

        ts_x -> anim 2000.0, OWS_SW = 2000, OWS_CLASH = -210
        ts_y -> anim    0.0, OWS_SW =    0, OWS_OS    =  220
        ts_w -> anim  400.0, OWS_SW =  400
        ts_h -> anim  200.0, OWS_SW =  200
        locally
            show anim (0.0, OWS_SW=0, OWS_CLASH=1, OWS_VIRT=0.5, OWS_CNTN=0)
            soft_box ts_x, ts_y, ts_w, ts_h, redhat_blue2, "Their software" fr "Leur logiciel"
            soft_box ts_x + 420.0, ts_y, ts_w, ts_h, redhat_blue2, "Some software" fr "Un logiciel"
            soft_box ts_x + 840.0, ts_y, ts_w, ts_h, redhat_blue2, "Other software" fr "Un autre logiciel"

        at_step OWS_OS
        os_x -> anim    0.0, OWS_SW =    0
        os_y -> anim    0.0, OWS_SW =    0, OWS_VIRT = -60, OWS_CNTN =   0, OWS_KVIRT=-60
        os_w -> anim 1650.0, OWS_SW = 1650
        os_h -> anim  200.0, OWS_SW =  200, OWS_VIRT = 100, OWS_CNTN = 200, OWS_KVIRT=100
        soft_box os_x, os_y, os_w, os_h, redhat_green1, "Operating system" fr "Système d'exploitation"

        locally
            show anim (0.0, OWS_SW=0, OWS_CHROOT=1, OWS_VIRT=0)
            soft_box -550, 100, 200, 200, redhat_red2,
                paragraph "chroot"
                image 0, 0, 50%, 50%, "Hard Disk.png"

        at_step OWS_VIRT
        vi_x -> anim -630.0, OWS_SW = -630,                 OWS_CNTN = -5000, OWS_KVIRT=-630
        vi_y -> anim   60.0, OWS_SW =   60, OWS_VIRT = 60
        vi_w -> anim  400.0, OWS_SW =  400
        vi_h -> anim  200.0, OWS_SW =  200, OWS_VIRT = 180
        for I in 0..integer(COUNT)-1 loop locally
            translate I * 420 mod 1680 + I / 4 * 30, I / 4 * 50, -I/4 * 200
            show exp(-0.1 * I)
            soft_box vi_x, vi_y, vi_w, vi_h, redhat_orange1, "Virtual Machine" fr "Machine virtuelle"

        vm_x -> anim 0.0, OWS_SW = 0, OWS_VIRT = -630
        vm_y -> anim 0.0, OWS_SW = 0, OWS_VIRT =  180
        vm_s -> anim 1.0, OWS_SW = 1, OWS_VIRT =   25%
        vm_c -> anim 1.0, OWS_SW = 1, OWS_CNTN = 0, OWS_KVIRT=1
        for I in 0..integer(COUNT)-1 loop locally
            translate I * 420 mod 1680 + I / 4 * 30, I / 4 * 50, -I/4 * 200
            translate vm_x, vm_y, 10
            scale vm_s
            soft_box os_x, os_y, os_w * vm_c, os_h * vm_c, redhat_green1, "Operating system" fr "Système d'exploitation"
            soft_box -630, -220, redhat_red2,
                image 0, 0, 60%, 60%, "Hard Disk.png"
            soft_box -210, -220, redhat_red2,
                image 0, 0, 30%, 30%, "CPU.png"
            soft_box 210,  -220, redhat_red2,
                image 0, 0, 30%, 30%, "RAM.png"
            soft_box 630, -220, redhat_red2,
                image 0, 0, 30%, 30%, "Network card.png"

        locally
            show anim (0.0, OWS_SW=0, OWS_QEMU=1, OWS_CNTN=0)
            qe_x -> anim -630.0, OWS_SW = -630, OWS_QEMU = -350, OWS_CNTN=-3000
            qe_y -> anim   60.0, OWS_SW =   60, OWS_QEMU =   50
            qe_w -> anim  400.0, OWS_SW =  400, OWS_QEMU =  300
            qe_h -> anim  180.0, OWS_SW =  180, OWS_QEMU =   80
            soft_box qe_x, qe_y, qe_w, qe_h, redhat_orange2, "qemu/libvirt"

        locally
            show anim (0.0, OWS_SW=0, OWS_KVM=1, OWS_CNTN=0)
            kv_x -> anim -630.0, OWS_SW = -630, OWS_KVM  = -630, OWS_CNTN=-3000
            kv_y -> anim   60.0, OWS_SW =   60, OWS_KVM  =  -30
            kv_w -> anim  400.0, OWS_SW =  400, OWS_KVM  =  300
            kv_h -> anim  180.0, OWS_SW =  180, OWS_KVM  =   80
            soft_box kv_x, kv_y, kv_w, kv_h, redhat_orange3, "kvm"

        at_step OWS_MINI
        qm_x -> anim -630.0, OWS_SW = -630, OWS_MINI= -630
        qm_y -> anim  -20.0, OWS_SW =  -20, OWS_MINI=  -20
        qm_w -> anim  250.0, OWS_SW =  250, OWS_MINI=  250
        qm_h -> anim   80.0, OWS_SW =   80, OWS_MINI=   80
        for I in 0..integer(COUNT)-1 loop locally
            translate I * 420 mod 1680 + I / 4 * 30, I / 4 * 50, -I/4 * 200
            soft_box qm_x, qm_y, qm_w, qm_h, redhat_orange2, "qemu-mini"

    locally
        show anim (0.0, OWS_SW=0, OWS_DOCKER=1, OWS_KVIRT=0, OWS_KATA=1)
        do_x -> anim    0.0, OWS_SW =    0, OWS_DOCKER =    0
        do_y -> anim  300.0, OWS_SW =  300, OWS_DOCKER =  300
        do_w -> anim 1100.0, OWS_SW = 1100, OWS_DOCKER = 1100
        do_h -> anim  200.0, OWS_SW =  280, OWS_DOCKER =  200
        soft_box do_x, do_y, do_w, do_h, redhat_ocean1,
            image 0, 20, 20%, 20%, "Logo-Red_Hat-Marketplace-Operated_By_IBM-B-Standard-RGB.png"

    at_step OWS_K8S
    ku_x -> anim -650.0, OWS_SW = -650, OWS_DOCKER = -750
    ku_y -> anim   30.0, OWS_SW =   30, OWS_DOCKER =   30
    ku_w -> anim  300.0, OWS_SW =  300, OWS_DOCKER =  300
    ku_h -> anim  700.0, OWS_SW =  780, OWS_DOCKER =  700
    soft_box ku_x, ku_y, ku_w, ku_h, redhat_yellow1,
        image 0, 20, 25%, 25%, "OpenShift.png"

    at_step OWS_CRI
    cr_x -> anim -550.0, OWS_SW = -550, OWS_CRI = -550
    cr_y -> anim  -20.0, OWS_SW =  -20, OWS_CRI =  -20, OWS_KVIRT=-120
    cr_w -> anim  200.0, OWS_SW =  200, OWS_CRI =  200
    cr_h -> anim   80.0, OWS_SW =   80, OWS_CRI =   80
    soft_box cr_x, cr_y, cr_w, cr_h, redhat_orange4, "CRI"


// ============================================================================
//
//   Confidential Containers
//
// ============================================================================

CNF_START       -> 0            // Blank
CNF_HOST        -> 1            // Show the host
CNF_RESOURCES   -> 2            // Resources
CNF_CONTAINERS  -> 3            // Containers
CNF_SANDBOX_OUT -> 4            // Sandboxing going out
CNF_SANDBOX_IN  -> 5            // Sandboxing going in
CNF_TENANTS     -> 6            // Multiple tenants
CNF_SAFE_TODAY  -> 7            // What is safe today
CNF_MEMORY_VM   -> 8            // Memory in the guest
CNF_MEMORY_HOST -> 9            // Memory encryption
CNF_INTEGRITY   -> 10           // State integrity
CNF_ATTESTATION -> 11           // Attestation
CNF_LAST        -> 11

confidential Step:integer ->
// ----------------------------------------------------------------------------
//   Overview diagram for "simplified overview" slide
// ----------------------------------------------------------------------------
    anim_step Step

    locally
        light 0
        light_position 50, 50, 120

        rotate_x 10
        rotate_y 10 * sin (0.3 * time)

        locally
            color "lightgray"
            scale fade_at(smooth_step, CNF_HOST)
            cube 0, 0, -150, 1500, 400, 300

            color "black"
            text_box 0,0, 1500, 400,
                style "story"
                align 90%
                vertical_align 95%
                $ "Virtual machine host" fr "Hébergeur de machines virtuelles"

        locally
            color safe_color CNF_ATTESTATION
            translate 0, -270, 0
            scale fade_at(smooth_step, CNF_RESOURCES)
            cube 0, 0, -150, 1500, 100, 300

            color "black"
            text_box 0,0, 1500, 100,
                style "story"
                color "lightgray"
                align 90%
                vertical_align 95%
                $ "Physical resources" fr "Ressources physiques"

            translate -300, 0, 25
            scale 50%
            extrude_depth 20
            extrude_radius 20
            soft_box -630, -20, safe_color CNF_INTEGRITY,
                image 0, 0, 30%, 30%, "CPU.png"
            soft_box -210, -20, safe_color CNF_MEMORY_HOST,
                image 0, 0, 30%, 30%, "RAM.png"
            soft_box  210,  -20, safe_color CNF_SAFE_TODAY,
                image 0, 0, 60%, 60%, "Hard Disk.png"
            soft_box  630, -20, safe_color CNF_SAFE_TODAY,
                image 0, 0, 50%, 50%, "Network card.png"


        locally
            CONTAINERS_COUNT -> anim 0.0, CNF_START=0, CNF_CONTAINERS=12
            for I in  0..integer(CONTAINERS_COUNT)-1 loop locally
                color_hsv I * 37, 80%, 80%
                cube -650 + 120 * I, 0, 100, 100, 100, 100

            at_step CNF_CONTAINERS
            color "black"
            text_box 0, 50, 1500, 100,
                style "story"
                align 50%
                vertical_align 5%
                $ "Containers" fr "Conteneurs"


        locally
            extrude_depth 50
            translate -410, -50, 200
            rotate_x -40
            locally
                color "red"
                rotate_z -90
                scale fade_between(smooth_step, CNF_SANDBOX_OUT, CNF_SANDBOX_IN)
                arrow 0, 0, 100, 100, 50, 60%
                translate_z 20
                rotate_z 45
                cube 0, 0, 0, 120, 10, 10
                rotate_z 90
                cube 0, 0, 0, 120, 10, 10
            locally
                translate 120, 0, 0
                color "green"
                rotate_z 90
                scale fade_between(smooth_step, CNF_SANDBOX_IN, CNF_SANDBOX_IN)
                arrow 0, 0, 100, 100, 50, 60%
                color "white"
                rotate_z 90
                extrude_depth 0
                image 0, 90 + 50 * sin(1.1*time), 10%, 10%, "BlueEyeIcon.png"

        locally
            show fade_between(smooth_step, CNF_TENANTS, CNF_TENANTS)
            translate -120, 0, 150
            for I in 0..3 loop
                image I * -120 - 60, 0, 1%, 1%, "Apple.png"
            image 320, 0, 10%, 10%, "Samsung.png"
            image 520, 0, 10%, 10%, "Samsung.png"

        memory_cone Color, X, Y, Z, Start, Stop, Body ->
            locally
                color Color
                translate X, Y, Z
                locally
                scale fade_between(smooth_step, Start, Stop)
                rotate_y 10 * sin (0.32 * time)
                rotate_x 150
                truncated_cone 0, 0, -200, 100, 100, 400, 0.0
                rotate_x 180
                translate_z 400
                frame_texture 512, 150,
                    color "white"
                    rectangle 999, 999
                    text_box 0, 0, 450, 200,
                        style "story"
                        align 50%
                        vertical_align 50%
                        Body
                texture_transform
                    scale 1, 3, 1
                    translate_y -0.3 * time mod 1
                    texture_wrap true, true
                ellipse 0, 0, 200, 200

        locally
            refresh 0
            memory_cone "lightblue", -290, 0, 120, CNF_MEMORY_VM, CNF_INTEGRITY,
                text "Hello World, value " fr "Hello, la valeur " & integer(time * 40 mod 1000)
                line_break
                text "is absolutely secret!" fr "est un secret!"
            memory_cone "lightgreen", -410, -250, 0, CNF_MEMORY_HOST, CNF_MEMORY_HOST,
                text "h3110 W0r1D; vA1u3 " fr "h3110; 1Ӓ √@Ĺ€ŮŘ " & integer(time * 40 mod 997)
                line_break
                text "15 @B501ùT31y 53cR3t¡" fr "35T ùN 53kŕ37¡"

        only_at CNF_INTEGRITY,
            for I in 0..1 loop locally
                translate -600 + 200 * I, -200 + 20 * sin((3.41 + I) * time), 100

                locally
                    color "white"
                    translate_z 125
                    extrude_depth 20
                    rectangle 0, 120, 180, 120
                    extrude_depth 0
                    text_box 0, 120, 150, 100,
                        font "Menlo", 14
                        color "darkblue"
                        align 0
                        if I = 0 then
                            $ "RAX = 0x0BADDA7A"
                            $ "RCX = 0x0BADC0DE"
                            $ "CR3 = 0xDEADBEEF"
                        else
                            $ "while (*ptr++=0);"
                            $ "if (!dead)"
                            $ "    goto retry;"

                rotate_z -90
                color "red"
                scale fade_between(smooth_step, CNF_INTEGRITY, CNF_INTEGRITY)
                arrow 0, 0, 100, 100, 50, 60%
                color "darkred"
                translate_z 20
                rotate_z 45
                cube 0, 0, 0, 120, 10, 10
                rotate_z 90
                cube 0, 0, 0, 120, 10, 10

        only_at CNF_ATTESTATION,
            color "green"
            font "Arial Unicode MS", 160
            align 50%
            vertical_align 50%
            extrude_depth 55
            translate_z 50
            text_box 700, -210, 100, 100, { text "✓" }
            text_box 700,  250, 100, 100, { text "✓" }
            translate_z 140
            text_box -360,  80, 100, 100, { text "✓" }
            text_box 480,   80, 100, 100, { text "✓" }
            text_box 360,   80, 100, 100, { text "✓" }

     at_step CNF_LAST
     if integer(smooth_step + 0.5) in CNF_MEMORY_VM..CNF_INTEGRITY then refresh 0

     safe_color Limit ->  if integer(smooth_step + 0.5) < Limit then redhat_red1 else "darkgreen"



// ============================================================================
//
//    Kata Containers animation
//
// ============================================================================

KC_OPENSHIFT    ->  1
KC_KUBELET      ->  2
KC_CRI          ->  3
KC_RUNC         ->  4
KC_CONTAINER    ->  5
KC_CNI_AND_CSI  ->  6
KC_STORAGE      ->  7
KC_KATA_SHIM    ->  8
KC_HYPERVISOR   ->  9
KC_VM           -> 10
KC_KATA_AGENT   -> 11
KC_KATA_CTR     -> 12
KC_CTR_ECOSYS   -> 13
KC_VM_ISOL      -> 14

KC_CONFIDENTIAL -> KC_VM_ISOL + 1
KC_LINUX_KERNEL -> KC_CONFIDENTIAL + CC_LINUX_KERNEL
KC_FIRMWARE     -> KC_CONFIDENTIAL + CC_FIRMWARE
KC_SHIM_V2      -> KC_CONFIDENTIAL + CC_SHIM_V2
KC_CC_HYPERVISOR-> KC_CONFIDENTIAL + CC_HYPERVISOR
KC_ENCRYPTING   -> KC_CONFIDENTIAL + CC_ENCRYPTING
KC_RELYING      -> KC_CONFIDENTIAL + CC_RELYING
KC_PLATFORM     -> KC_CONFIDENTIAL + CC_PLATFORM
KC_HOST         -> KC_CONFIDENTIAL + CC_HOST
KC_TENANT       -> KC_CONFIDENTIAL + CC_TENANT

CC_LINUX_KERNEL ->  1
CC_FIRMWARE     ->  2
CC_SHIM_V2      ->  3
CC_HYPERVISOR   ->  4
CC_ENCRYPTING   ->  5
CC_RELYING      ->  6
CC_PLATFORM     ->  7
CC_HOST         ->  8
CC_TENANT       ->  9

KC_ENABLEMENT   -> KC_TENANT + 1
KC_HARDWARE     -> KC_ENABLEMENT + HE_HARDWARE
KC_FIRMWARE_CC  -> KC_ENABLEMENT + HE_FIRMWARE_CC
KC_RUNTIME_CC   -> KC_ENABLEMENT + HE_RUNTIME_CC
KC_HYPER_CC     -> KC_ENABLEMENT + HE_HYPER_CC
KC_KERNEL       -> KC_ENABLEMENT + HE_KERNEL
KC_AGENTCC      -> KC_ENABLEMENT + HE_AGENTCC
KC_ENABLEMENT_C -> KC_ENABLEMENT + HE_COMPLETE
HE_HARDWARE     -> 1
HE_FIRMWARE_CC  -> 2
HE_KERNEL       -> 3
HE_RUNTIME_CC   -> 4
HE_HYPER_CC     -> 5
HE_AGENTCC      -> 6
HE_COMPLETE     -> 7

KC_PULLIMAGE    -> KC_ENABLEMENT_C + 1
KC_INSIDE_GUEST -> KC_PULLIMAGE + PI_INSIDE_GUEST
KC_ENCRYPTED    -> KC_PULLIMAGE + PI_ENCRYPTED
KC_API_CHANGE   -> KC_PULLIMAGE + PI_API_CHANGE
KC_PULLIMAGE_C  -> KC_PULLIMAGE + PI_COMPLETE
PI_INSIDE_GUEST -> 1
PI_ENCRYPTED    -> 2
PI_API_CHANGE   -> 3
PI_COMPLETE     -> 4

KC_ATTEST       -> KC_PULLIMAGE_C + 1
KC_PREATTEST    -> KC_ATTEST + AT_PREATTEST
KC_POSTATTEST   -> KC_ATTEST + AT_POSTATTEST
KC_WORKLOAD     -> KC_ATTEST + AT_WORKLOAD
AT_PREATTEST    -> 1
AT_POSTATTEST   -> 2
AT_WORKLOAD     -> 3

KC_MSMTS        -> KC_WORKLOAD + 1
KC_MEASURING    -> KC_MSMTS + MS_MEASURING
KC_CHALLENGE    -> KC_MSMTS + MS_CHALLENGE
KC_KEY_DELIVERY -> KC_MSMTS + MS_KEY_DELIVERY
KC_REMOTE       -> KC_MSMTS + MS_REMOTE
MS_MEASURING    -> 1
MS_CHALLENGE    -> 2
MS_KEY_DELIVERY -> 3
MS_REMOTE       -> 4


transfer_gradient Scale:real, Speed: real ->
// ----------------------------------------------------------------------------
//   Animated gradient for "marching ants" lines
// ----------------------------------------------------------------------------
    linear_gradient 0, 0, 256, 0, 256, 256,
        gradient_color 0%, "darkgray", 0.3
        gradient_color 10%, "darkgray", 0.9
        gradient_color 20%, "lightgray", 0.3
        gradient_color 50%, "white", 0.9
        gradient_color 80%, "darkgray", 0.9
        gradient_color 90%, "lightgray", 0.3
        gradient_color 100%, "darkgray", 0.0
    transfer_gradient Scale, Speed, 0
transfer_gradient Scale:real, Speed:real, Angle:real->
    texture_wrap true, true
    texture_transform
        translate_x -Speed*time mod 1
        rotate_z Angle
        scale_x Scale


marching_ants Step:integer, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//   Draw marching-ants style box
// ----------------------------------------------------------------------------
    at_anim Step
    if anim_step = Step then
        line_width 16
        linear_gradient 0, 0, 256, 0, 256, 256,
            gradient_color 0%, "darkgray", 0.3
            gradient_color 10%, "gray", 0.9
            gradient_color 20%, "lightgray", 0.3
            gradient_color 50%, "white", 0.9
            gradient_color 80%, "gray", 0.9
            gradient_color 90%, "lightgray", 0.3
            gradient_color 100%, "darkgray", 0.0
        locally
            transfer_gradient W / 100, 2.5,  0
            line X-8-W/2, Y+0+H/2, X+8+W/2, Y+0+H/2
        locally
            transfer_gradient H / 100, 2.5, 90
            line X+0+W/2, Y-8+H/2, X+0+W/2, Y+8-H/2
        locally
            transfer_gradient W / 100, 2.5, 180
            line X+8+W/2, Y-0-H/2, X-8-W/2, Y-0-H/2
        locally
            transfer_gradient H / 100, 2.5, 270
            line X-0-W/2, Y+8-H/2, X-0-W/2, Y-8+H/2
    else
        line_width 8
        rectangle X, Y, W, H


security_frame Color, DarkColor, X, Y, W, H, Icon, IconScale ->
// ----------------------------------------------------------------------------
//   Draw a little yellow/black box with a padlock
// ----------------------------------------------------------------------------
    background 0,
        line_width 4
        line_color Color
        color "transparent"
        translate_z 2
        rectangle X, Y, W, H
        line_color DarkColor
        rectangle X, Y, W+10, H+10
        rectangle X, Y, W-10, H-10
        color "white"
        line_color "transparent"
        translate_z 5
        image X + W/2, Y + H/2, IconScale, IconScale, Icon

SECURITY_TOP    -> anim 60.0, KC_PREATTEST=60, KC_POSTATTEST=150, KC_WORKLOAD=360
SECURITY_Y      -> (SECURITY_TOP - 40) / 2
SECURITY_H      -> (SECURITY_TOP + 40)
ATTESTATION_TARGET_Y -> anim 0.0, 0=-130, KC_POSTATTEST=10, KC_WORKLOAD=200
RELYING_X       -> anim 1100.0, 0=1100, KC_ATTEST=800


kata_containers Step:integer ->
// ----------------------------------------------------------------------------
//   Representation of kata containers
// ----------------------------------------------------------------------------
    picture
        anim_step Step
        adjust_diagram

        if Step < KC_MSMTS then
            at_anim KC_OPENSHIFT
            image -700, 370, 12%, 12%, "Logo-Red_Hat-Marketplace-Operated_By_IBM-B-Standard-RGB.png"
            at_anim KC_KUBELET
            framed_box "#38E", -700, 300, 300, 80, "Kubelet"
            at_anim KC_CRI
            framed_box "#38E", -250, -300, 1200, 80, "CRI (cri-o or containerd)" fr "CRI (cri-o ou containerd)"
            locally
                line_color "#38E"
                line_width 15
                line_arrow -700, 280, none, -700, -280, arrowhead

            at_anim KC_RUNC
            framed_box "#14A",  -300, -200, 400, 80, "Runtime (runc)"
            at_anim KC_CONTAINER
            framed_box "#026",  -300, -50, 400, 200, ""
            framed_box "#026",  -300, -50, 380, 180, "Container" fr "Conteneur"

            at_anim KC_CNI_AND_CSI
            framed_box "#38E",  490, -300, 220, 80, "CNI"
            framed_box "#38E",  740, -300, 220, 80, "CSI"
            image 490, -220, 40%, 40%, "Network card.png"
            image 740, -220, 50%, 50%, "Hard Disk.png"

        if Step < KC_ATTEST then
            at_anim KC_STORAGE
            locally
                line_color anim_text("#38E", KC_ENCRYPTING = "#3A5")
                color anim_text("#38E", KC_ENCRYPTING = "#3A5"), 80%
                line_width 5
                rounded_rectangle 740, 120, 160, 140, 20

            locally
                color "#CDF"
                image 760, 140, 40%, 40%, "Hard Disk.png"
                image 740, 120, 40%, 40%, "Hard Disk.png"
                image 720, 100, 40%, 40%, "Hard Disk.png"

            locally
                line_color "#38E"
                line_width 15
                line_arrow 740, 60, arrowhead, 740, -180, arrowhead

            text_box 500, 120, 250, 160,
                color "#02B"
                font "Overpass", "Arial", 30
                align 100%
                vertical_align 50%
                if anim_step >= KC_ENCRYPTING then
                    color "#082"
                    $ "Encrypted" fr "Chiffrage des"
                $ "Images &"
                $ "Volumes"

        if Step <= KC_PULLIMAGE_C then
            locally
                at_anim KC_API_CHANGE
                line_color "#A22", 90%
                line_width 15
                line_arrow 320, -280, none, 320, 50, arrowhead

        at_anim KC_KATA_SHIM
        framed_box anim_text("#1A4", KC_SHIM_V2 = "#A21"),   150, -200, 400, 80, "Runtime (shim-v2)"
        at_anim KC_HYPERVISOR
        framed_box anim_text("#061", KC_CC_HYPERVISOR = "#610"),   150, -100, 400, 80, "Hypervisor (qemu)"
        at_anim KC_VM
        locally
            framed_box "#3C6",   150, 160, 420, 400, { vertical_align 100%; $ "VM (Linux)" }
            at_anim KC_KATA_AGENT
            framed_box "#062",   150, 100, 400,  80, "Kata Agent"
            at_anim KC_KATA_CTR
            framed_box "#062",   150, 250, 400, 200, ""
            framed_box "#062",   150, 250, 380, 180, "Container" fr "Conteneur"
            if Step = KC_PREATTEST then
                locally
                    color "white", 0.8 + 0.1 * sin(5 * time)
                    line_color "transparent"
                    texture ""
                    rectangle 150, 160, 430, 430
                    color "white"
                    rectangle 150, 250, 430, 350


        if Step < KC_MSMTS then
            at_anim KC_LINUX_KERNEL
            framed_box "#E22",     0, -400, 1700, 80, "Linux kernel" fr "Noyau Linux"
            at_anim KC_FIRMWARE
            framed_box "#811",     0, -500, 1700, 80, "Firmware / Hardware" fr "Microgiciel / Matériel"

        only_at_anim KC_PREATTEST..KC_WORKLOAD,
            security_frame "yellow", "black", 150, SECURITY_Y, 415, SECURITY_H, "Padlock-16.png", 30%
        only_at_anim KC_MEASURING..KC_KEY_DELIVERY,
            security_frame "yellow", "black", 150, SECURITY_Y, 415, SECURITY_H, "Padlock-16.png", 30%
        only_at_anim KC_PREATTEST..KC_WORKLOAD,
            line_color "#E22"
            color "transparent"
            line_width 20
            line_arrow 320, ATTESTATION_TARGET_Y, arrowhead, 320, -470, arrowhead
            marching_ants KC_PREATTEST, 150, -100, 400, 80

        at_anim KC_RELYING
        locally
            line_color "#0A4"
            color "#0A4", 20%
            translate RELYING_X, 250, 0
            path
                move_to 0, 100
                for I in 1..16 loop
                   quad_to (230 + 5 * sin I) * sin((I - 0.5) * pi / 8), (120 + 7 * sin I) * cos((I - 0.5) * pi / 8), 200 * sin(I * pi / 8), 100 * cos(I * pi / 8)
        if Step <= KC_PULLIMAGE then
            locally
                line_color "#0A4"
                line_width 8
                line_arrow 350, 250, arrowhead, 920, 250, arrowhead

        text_box RELYING_X - 20, 250, 300, 80,
            style "story"
            font_size 30
            color "#081"
            align 0.0
            vertical_align 0.5
            $ "Relying party" fr "Contrôleur"
        show 0.99
        framed_box "#0A4",   150 + RELYING_X, 300, 280, 80, {font_size 26; text "Key broker" fr "Accès aux clefs" }
        framed_box "#0A4",   150 + RELYING_X, 200, 280, 80, {font_size 26; text "Attestation service" fr "Serveur d'attestation" }

        if Step <= KC_ENABLEMENT_C then
            background -50,
                line_color "#E22"
                color "transparent"
                marching_ants KC_HARDWARE,      0, -500, 1700, 80
                marching_ants KC_FIRMWARE_CC,   0, -500, 1700, 80
                marching_ants KC_KERNEL,        0, -400, 1700, 80
                marching_ants KC_RUNTIME_CC,  150, -200, 400, 80
                marching_ants KC_HYPER_CC,    150, -100, 400, 80
                line_color "#082"
                marching_ants KC_AGENTCC,     150, 100, 400,  80

        if Step <= KC_PULLIMAGE_C then
            at_anim KC_INSIDE_GUEST
            locally
                transfer_gradient (920-350)*0.01, -2.5
                line_color "#0A4"
                line_width 8
                line_arrow 350, 250, arrowhead, 920, 250, arrowhead
            locally
                translate 920 - 300 * page_time mod 570, 250, 10
                image 0, 0, 20%, 20%, "Container Package.png"

            at_anim KC_ENCRYPTED
            locally
                transfer_gradient (520-350)*0.01, 2.5/ 3
                line_color "#0A4"
                line_width 8
                line_arrow 350, 100, arrowhead, 520, 100, arrowhead
            locally
                translate 350 + 100 * page_time mod 170, 100, 10
                image 0, 0, 10%, 10%, "Container Package.png"

            at_anim KC_API_CHANGE
            locally
                translate -720, 0, 0
                rotate_z 90
                text_box 0, 10, 300, 80,
                    style "story"
                    align 0.5
                    vertical_align 0.5
                    font_size 35
                    code "PullImage"

        only_at_anim KC_CHALLENGE,
            image 440 + time * 0.5 mod 1 * (RELYING_X - 290), -20, 0.8,0.8, "ID Card.jpg"
            line_width 15
            line_color "#0A4"
            color "transparent"
            path
                endpoints_style none, arrowhead
                move_to 350,             100
                line_to 150 + RELYING_X, 100
                line_to 150 + RELYING_X, 180
            line_color "#082"
            marching_ants KC_CHALLENGE, 150 + RELYING_X, 200, 280, 80

        only_at_anim KC_KEY_DELIVERY,
            line_width 15
            line_color "#0A4"
            color "transparent"
            path
                endpoints_style arrowhead, none
                move_to 350,             100
                line_to 150 + RELYING_X, 100
                line_to 150 + RELYING_X, 280
            line_color "#082"
            marching_ants KC_KEY_DELIVERY, 150 + RELYING_X, 300, 280, 80
            line_width 0
            color "white"
            image 440 + time * -0.5 mod 1 * (RELYING_X - 290), 20, 0.4,0.4, "Keys.png"

        only_at_anim KC_REMOTE,
            line_width 15
            line_color "#F33"
            color "transparent"
            path
                endpoints_style arrowhead, none
                move_to 350,             100
                line_to 150 + RELYING_X, 100
                line_to 150 + RELYING_X, 160
            line_color "#F33"
            marching_ants KC_REMOTE, 150 + RELYING_X, 200, 280, 80
            line_width 0
            color "white"
            image 440 + time * -0.5 mod 1 * (RELYING_X - 290), 20, 0.4,0.4, "T1000 Just Says No.jpg"




// ============================================================================
//
//   Utlities
//
// ============================================================================

caption_at TimeSlot, Body ->
// ----------------------------------------------------------------------------
//   Show the caption at the given time
// ----------------------------------------------------------------------------
    only_at TimeSlot,
        soft_box -700, 500, 400, 200, "#444", Body


page_quote N ->
// ----------------------------------------------------------------------------
//   Show a selected quote in a page
// ----------------------------------------------------------------------------
    locally
        show_quotes step
        at_step 1
    on "pageentry",
        show_quote N
    on "pageexit",
        show_quotes_v := 0.0
        show_quotes := 0.0


show_quote N    ->
// ----------------------------------------------------------------------------
//   Show a selected quote from keyboard
// ----------------------------------------------------------------------------
    show_quotes := 1
    quote_step N
    refresh 0.1
    on "pageexit",
        show_quotes_v := 0.0
        show_quotes := 0.0

show_quotes     -> 0.0
show_quotes S   -> show_quotes := S
show_quotes_v   -> 0.0

interpolate 10%, show_quotes, show_quotes_v
if show_quotes_v > 0.0 then
    show show_quotes_v
    quotes
        quote "Gretchen Rubin", "I love cunning containers as much as anyone, but I've found that if I get rid of everything I don't need, I often don't need a container at all."
        quote "Andrew Tannenbaum", "The good thing about standards is", " that there are so many to choose from."
        quote "Edward Teller", "A state-of-the-art calculation requires 100 hours of CPU time on the state-of-the-art computer, independent of the decade."
        quote "Will Durant", "History is always repeating itself,", "but each time the price goes up"
        quote "Henry Louis Mencken", "For every problem, there is a solution that is simple, clean and wrong"
        quote "Julian Assange", "A nation can't solve", "what the press won't let it perceive"
disabled
        quote "Second law of thermodynamics", "Entropy, i.e. disorder,  always increases with time"
        quote  "Perl motto (Larry Wall)", "There is more than one way to do it"
        quote "Richard P. Gabriel", "Get half of the right thing available so that it spreads like a virus. Once people are hooked on it, improve it to 90% of the right thing."
        quote "Fabiano Fidêncio", "Break it properly so that you can fix it properly"

key "q" -> show_quotes (if show_quotes <> 0.0 then 0.0 else 1.0); refresh 0
key "]" -> quote_step := quote_step + 1; refresh 0
key "[" -> quote_step := quote_step - 1; refresh 0
